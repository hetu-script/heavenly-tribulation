import '../game.ht'
import 'character/character.ht'

/// 历史事件记录
struct Incident {
  construct ({
    content: str,
    isPrivate: bool = false,
    isGlobal: bool = false,
    order = 0,
    ideal = 0,
    good = 0,
    score = 0, // 额外的好感分，加到本事件的行动者身上
    subjectIds = [],
    objectIds = [],
    witnessIds = [],
    // heardIds = [],
    withItem: str,
    toItem: str,
    locationId: str,
    terrainIndex: int,
  }) {
    assert(content != null && content.isNotEmpty)

    this.index = history.length
    this.content = content
    this.timestamp = game.timestamp
    this.order = order
    this.ideal = ideal
    this.good = good
    this.score = score
    this.subjectIds = subjectIds
    this.objectIds = objectIds
    this.witnessIds = witnessIds
    // this.heardIds = heardIds
    this.withItem = withItem
    this.toItem = toItem
    this.locationId = locationId
    this.terrainIndex = terrainIndex
    this.isPrivate = isPrivate
    this.isGlobal = isGlobal
    history.add(this)

    for (final id in subjectIds) {
      final char = game.characters[id]
      characterExperiencedIncident(char, this)
    }

    for (final id in objectIds) {
      final char = game.characters[id]
      characterExperiencedIncident(char, this)
    }

    for (final id in witnessIds) {
      final char = game.characters[id]
      characterWitnessIncident(char, this, witnessed: true)
    }

    // for (final id in heardIds) {
    //   final char = game.characters[id]
    //   characterWitnessIncident(char, this)
    // }

    if (isGlobal) {
      for (final char of game.characters) {
        if (char.id in subjectIds || char.id in objectIds || char.id in witnessIds) continue
        characterWitnessIncident(char, this)
      }
    } else if (!isPrivate) {
      if (locationId) {
        final location = game.locations[locationId]
        for (final id in location.dwellingCharacterIds) {
          if (id in subjectIds || id in objectIds || id in witnessIds) continue
          final char = game.characters[id]
          characterWitnessIncident(char, this)
        }
      }
    }

    engine.info('触发事件：${content}')
  }
}

fun getIncidentByIndex(index: int) {
  return history[index]
}
