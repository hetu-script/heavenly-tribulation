import '../common.ht'
import '../effects.ht'

const kItemDefaultIcon = 'assets/images/unknown_item.png'
// final kDefaultItemDescription = getLocaleString('defaultItemDescription')

const kUnknownItemIcon = 'icon/unknown_item.png'

const kEquipTypeOffense = 'offense'
const kEquipTypeSupport = 'support'
const kEquipTypeArcane = 'arcane'
const kEquipTypeTalisman = 'talisman'
const kEquipTypeCompanion = 'companion'
// const kEquipTypeArmor = 'armor'
// const kEquipTypeBoots = 'boots'
// const kEquipTypeManeuver = 'maneuver'

const kEquipmentMax = 5
// const kOffenseEquipmentMax = 5
// const kSupportEquipmentMax = 4
// const kArcaneEquipmentMax = 4
// const kTalismanEquipmentMax = 4
// const kCompanionEquipmentMax = 5

final kRarity2ItemLevels = {
  common: 10, // common 白
  uncommon: 20, // uncommon 灰
  rare: 30, // rare 蓝
  epic: 40, // epic 紫
  legendary: 50, // legendary 橙
  mythic: 60, // mythic 金
  exotic: 70, // exotic 虹
}

fun calculateItemStats(item) {
  item.stats = item.attributes.clone()
}

fun characterAcquireMoney(character, value) {
  assert(value > 0)
  character.money += value
  final incidentContent = getLocaleString('characterAcquireMoney', [ character.name, value ])
  Incident(
    content: incidentContent,
    subjectIds: [character.id],
    isPrivate: true,
  )
}

// 角色获得物品，此函数并不检查物品本身是否在其他角色的物品栏中
fun characterAcquire(character, item) {
  if (item.rarity == kCommon && character.inventory.containsKey(item.id)) {
    character.inventory[item.id].stackSize += 1
  } else {
    character.inventory[item.id] = item
  }

  final incidentContent = getLocaleString('characterAcquiredItem', [
    character.name,
    item.name,
  ])
  Incident(
    content: incidentContent,
    subjectIds: [character.id],
    isPrivate: true,
  )
}

fun characterLearn(character, skill) {
  character.skills[skill.id] = skill
}

// 使用食物或者丹药
// 返回值代表是否使用成功
// 此函数允许对一个不在物品栏中的物品调用
fun characterConsume(character, item) {
  if (!item.isConsumable) {
    engine.info('${item.name} 无法食用。')
    return false
  }

  if (character.inventory.containsKey(item.id)) {
    if (item.rarity == kCommon) {
      if (item.stackSize > 1) {
        --item.stackSize
      }
    } else {
      delete character.inventory[item.id]
    }
  }

  if (item.effects) {
    for (final effect in item.effects) {
      when (effect.name) {
        kEffectRestoreLife -> {
          character.stats.life += effect.values.first.value
        }
        kEffectRestoreChi -> {
          character.stats.chi += effect.values.first.value
        }
        kEffectRestoreStamina -> {
          character.stats.stamina += effect.values.first.value
        }
      }
    }
  }

  var incidentContent
  when (item.kind) {
    kEntityConsumableKindBeverage -> {
      incidentContent = getLocaleString('characterDrankBeverage', [
        character.name,
        item.name,
      ])
    }
    kEntityConsumableKindMedicine -> {
      incidentContent = getLocaleString('characterTakeMedicine', [
        character.name,
        item.name,
      ])
    }
    else -> {
      incidentContent = getLocaleString('characterAteFood', [
        character.name,
        item.name,
      ])
    }
  }

  Incident(
    content: incidentContent,
    subjectIds: [character.id],
    isPrivate: true,
  )
  
  return true
}

// 法宝和技能都可以装备，只要类型符合
// 返回值代表是否装备成功
// 如果对一个不在物品栏中的物品调用，会先将其加入物品栏
// 
// 装备并非直接将物品数据放在人物上，而是用下面的数据结构
// {
//   id,
//   entityType,
//   category,
//   kind,
//   equipType,
//   life,
//   supports,
// }
fun characterEquip(character, entity, { index }) -> bool {
  if (!entity.isEquippable) {
    engine.info('${entity.name} 无法装备。')
    return false
  }
  if (entity.equippedPosition) {
    engine.info('${entity.name} 已经在装备中了。')
    return false
  }
  var equipData = {
    id: entity.id,
    entityType: entity.entityType,
    category: entity.category, // 主类型：武器、斗技等
    kind: entity.kind, // 次类型：剑、拳法等
    equipType: entity.equipType,
    life: entity.stats.life,
    lifeMax: entity.stats.lifeMax,
  }
  when (entity.entityType) {
    kEntityTypeItem -> {
      if (!character.inventory[entity.id]) characterAcquire(character, entity)
    }
    kEntityTypeSkill -> {
      if (!character.skills[entity.id]) characterLearn(character, entity)
    }
    kEntityTypeNpc, kEntityTypeCharacter -> {
      if (!character.companions[entity.id]) characterGetCompanion(character, entity)
    }
  }
  
  when (entity.equipType) {
    // 剑法、心诀等支持型技能
    kEquipTypeSupport -> {
      for (final i in range(1, kSupportEquipmentMax)) {
        final oldEquipData = character.equipments[kEquipTypeOffense][i]
        if (oldEquipData && oldEquipData.category == data.category &&
            oldEquipData.kind == data.kind) {
          if (oldEquipData.supports.length < _kSupportEquipmentMax - 1) {
            oldEquipData.supports.add(equipData)
          } else {
            // 移除旧装备
            final oldEntity = getEquippedEntity(oldEquipData.supports.first, character)
            delete oldEntity.equippedPosition
            oldEquipData.supports.first = equipData
          }
          entity.equippedPosition = i
          return true
        }
      }
      return false
    }
    // kEquipTypeOffense: 斗技、武器、攻击法宝等
    // kEquipTypeArcane: 内功、轻功等
    // kEquipTypeTalisman: 护甲、靴子、辅助法宝等
    kEquipTypeOffense, kEquipTypeArcane, kEquipTypeTalisman, kEquipTypeCompanion -> {
      equipData.supports = []
      // 如果有空位就装备在空位上
      if (!index) {
        for (final i in range(1, kEquipmentMax)) {
          if (!character.equipments[entity.equipType][i]) {
            character.equipments[entity.equipType][i] = equipData
            entity.equippedPosition = i
            return true
          }
        }
        // 否则替换掉第一个空位
        index = 1
      }
      final oldEquipData = character.equipments[entity.equipType][1]
      final oldItem = getEquippedEntity(equipData, character)
      delete oldItem.equippedPosition
      character.equipments[entity.equipType][index] = equipData
      if (equipData.category == oldItem.category && equipData.kind == oldItem.kind) {
        equipData.supports = oldEquipData.supports
      } else {
        for (final support in equipData.supports) {
          delete support.equippedPosition
        }
      }
      entity.equippedPosition = index
      return true
    }
  }
}

fun characterUnequip(character, entity) {
  assert(entity.equippedPosition != null)
  when (entity.equipType) {
    // 剑法、心诀等支持型技能
    kEquipTypeSupport -> {
      character.equipments[kEquipTypeOffense][entity.equippedPosition]
        .supports.removeWhere((equipData) => equipData.id == entity.id)
    }
    kEquipTypeCompanion -> {
      characterDismissCompanion(character, entity)
    }
    // 斗技、武器、法宝等
    kEquipTypeOffense, kEquipTypeArcane, kEquipTypeTalisman -> {
      delete character.equipments[entity.equipType][entity.equippedPosition]
    }
  }
}

fun getEquippedEntity(equipData, character) {
  if (equipData == null) return null
  var entity
  when (equipData.entityType) {
    kEntityTypeItem -> {
      entity = character.inventory[equipData.id]
    }
    kEntityTypeSkill -> {
      entity = character.skills[equipData.id]
    }
    kEntityTypeNpc -> {
      entity = game.npcs[equipData.id]
    }
    kEntityTypeCharacter -> {
      entity = game.characters[equipData.id]
    }
  }
  assert(entity != null)
  return entity
}
