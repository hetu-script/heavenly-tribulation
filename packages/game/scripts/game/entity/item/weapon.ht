import '../../generator/name/name.ht' as nameGenerator
import '../constants.ht'
import 'constants.ht'
import '../../l10n.ht'

const kDefaultWeaponIcon = 'icon/item/sword.png'

const kSpeedShort = 5
const kSpeedBase = 20
const kSpeedVariable = 10
const kDamageMin = 15
const kDamageLow = 20
const kDamageMedium = 20
const kDamageHigh = 20
const kDamageDeviation = 10
const kDurabilityLow = 200
const kDurabilityMedium = 300
const kDurabilityHigh = 400
const kDurabilityVariable = 100

final kEffects = ['parry', 'counter']
const kEffectProbabilityBase = 0.05
const kEffectProbabilityVariable = 0.1

final kWeaponTypes = [
  'slashing',
  'bludgeoning',
  'piercing',
]

struct Weapon {
  construct ({
    name,
    rarity,
    value,
    icon,
    creatorId,
    createdTime,
    description,
    inscription,
    weaponType,
    attributes,
  }) {
    this.name = name
    this.category = 'weapon'
    this.isAttackItem = true
    this.rarity = rarity
    this.value = value ?? 0
    this.icon = icon ?? kDefaultWeaponIcon
    this.consumable = false
    this.equippable = true
    this.creatorId = creatorId
    this.createdTime = createdTime
    this.description = description ?? kDefaultItemDescription
    this.inscription = inscription
    this.type = weaponType ?? kWeaponTypes.random
    this.attributes = attributes
    
    if (!this.name) {
      final r = nameGenerator.getTalisman(1, category: this.type, rarity: this.rarity).first
      this.name = r.name
      this.rarity ??= r.rarity 
    }
    this.id = 'item_${Hash.uid4(2)}_${this.name}'
    
    if (!this.attributes) {
      when (this.type) {
        // 伤害一般，耐久低，出招速度一般，收招速度快，出招时自带格挡几率
        'slashing' -> {
          this.attributes = {
            durability: randomInt(kDurabilityLow, kDurabilityVariable),
            damage: normalDistribute(kDamageMedium, kDamageDeviation, kDamageMin),
            startUp: randomInt(kSpeedBase, kSpeedVariable),
            recovery: randomInt(kSpeedShort, kSpeedVariable),
            effects: {
              parry: [
                       { value: random(kEffectProbabilityBase, kEffectProbabilityVariable),
                         type: kValueTypePercentage
                       }
                     ],
            },
            abilities: [],
            levels: [],
          }
        }
        // 伤害高，耐久高，出招速度一般，收招速度一般，出招时自带破招和格挡几率
        'bludgeoning' -> {
          this.attributes = {
            durability: randomInt(kDurabilityHigh, kDurabilityVariable),
            damage: normalDistribute(kDamageHigh, kDamageDeviation, kDamageMin),
            startUp: randomInt(kSpeedBase, kSpeedVariable),
            recovery: randomInt(kSpeedBase, kSpeedVariable),
            effects: {
              parry: [
                       { value: random(kEffectProbabilityBase, kEffectProbabilityVariable),
                         type: kValueTypePercentage
                       }
                     ],
              counter: [
                       { value: random(kEffectProbabilityBase, kEffectProbabilityVariable),
                         type: kValueTypePercentage
                       }
                     ],
            },
            abilities: [],
            levels: [],
          }
        }
        // 伤害低，耐久一般，出招速度快，收招速度快，没有自带属性
        'piercing' -> {
          this.attributes = {
            durability: randomInt(kDurabilityMedium, kDurabilityVariable),
            damage: normalDistribute(kDamageLow, kDamageDeviation, kDamageMin),
            startUp: randomInt(kSpeedShort, kSpeedVariable),
            recovery: randomInt(kSpeedShort, kSpeedVariable),
            effects: {},
            abilities: [],
            levels: [],
          }
        }
      }
    }

    recalculateAttributesOfWeapon(this)
  }
}

fun recalculateAttributesOfWeapon(weapon) {
  weapon.stats = weapon.attributes
}
