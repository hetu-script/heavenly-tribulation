import '../../generator/name/name.ht' as nameGenerator
import '../constants.ht'
import 'constants.ht'
import '../../l10n.ht'
import '../../util.ht'

const kDefaultWeaponIcon = 'icon/item/sword.png'

const kSpeedShort = 5
const kSpeedBase = 20
const kSpeedVariable = 10
const kDamageMin = 10
const kDamageMax = 65
const kDamageLow = 15
const kDamageMedium = 30
const kDamageHigh = 50
const kDamageDeviation = 10
const kDurabilityLow = 200
const kDurabilityMedium = 300
const kDurabilityHigh = 400
const kDurabilityVariable = 100
final kEffects = ['parry', 'counter']
const kEffectProbabilityBase = 0.1
const kEffectProbabilityVariable = 0.1

final kWeaponTypes = [
  'slashing',
  'bludgeoning',
  'piercing',
]

struct Weapon {
  construct ({
    name,
    rarity,
    value,
    icon,
    creatorId,
    createdTime,
    description,
    inscription,
    weaponType,
    attributes,
  }) {
    this.name = name
    this.category = 'weapon'
    this.isAttackItem = true
    this.rarity = rarity
    this.value = value ?? 0
    this.icon = icon ?? kDefaultWeaponIcon
    this.consumable = false
    this.equippable = true
    this.creatorId = creatorId
    this.createdTime = createdTime
    this.description = description ?? kDefaultItemDescription
    this.inscription = inscription
    this.type = weaponType ?? kWeaponTypes.random
    this.isEquipped = false
    
    if (!this.name) {
      final r = nameGenerator.getTalisman(1, category: this.type, rarity: this.rarity).first
      this.name = r.name
      this.rarity ??= r.rarity 
    }
    this.id = 'item_${Hash.uid4(2)}_${this.name}'
    
    when (this.type) {
      // 伤害低，耐久一般，出招速度快，收招速度快
      'piercing' -> {
        final effects = {}
        this.attributes = {
          durability: Math.randomInt(kDurabilityVariable) + kDurabilityMedium,
          damage: Math.gaussianNoise(kDamageLow, kDamageDeviation, min: kDamageMin, max: kDamageMax),
          startUp: Math.randomInt(kSpeedVariable) + kSpeedShort,
          recovery: Math.randomInt(kSpeedVariable) + kSpeedShort,
          effects: effects,
          abilities: [],
          levels: [],
        }
      }
      // 伤害一般，耐久低，出招速度一般，收招速度快
      'slashing' -> {
        final effects = {
          parry: [
                    { value: Math.random() * kEffectProbabilityVariable + kEffectProbabilityBase,
                      type: kValueTypePercentage
                    }
                  ],
        }
        this.attributes = {
          durability: Math.randomInt(kDurabilityVariable) + kDurabilityLow,
          damage: Math.gaussianNoise(kDamageMedium, kDamageDeviation, min: kDamageMin, max: kDamageMax),
          startUp: Math.randomInt(kSpeedVariable) + kSpeedBase,
          recovery: Math.randomInt(kSpeedVariable) + kSpeedShort,
          effects: effects,
          abilities: [],
          levels: [],
        }
      }
      // 伤害高，耐久高，出招速度一般，收招速度慢
      'bludgeoning' -> {
        final effects = {
          parry: [
                    { value: Math.random() * kEffectProbabilityVariable + kEffectProbabilityBase,
                      type: kValueTypePercentage
                    }
                  ],
          counter: [
                    { value: Math.random() * kEffectProbabilityVariable + kEffectProbabilityBase,
                      type: kValueTypePercentage
                    }
                  ],
        }
        this.attributes = {
          durability: Math.randomInt(kDurabilityVariable) + kDurabilityHigh,
          damage: Math.gaussianNoise(kDamageHigh, kDamageDeviation, min: kDamageMin, max: kDamageMax),
          startUp: Math.randomInt(kSpeedVariable) + kSpeedShort,
          recovery: Math.randomInt(kSpeedVariable) + kSpeedBase,
          effects: effects,
          abilities: [],
          levels: [],
        }
      }
    }

    if (attributes) {
      this.assign(attributes)
    }

    recalculateAttributesOfWeapon(this)
  }
}

fun recalculateAttributesOfWeapon(weapon) {
  weapon.stats = weapon.attributes
}
