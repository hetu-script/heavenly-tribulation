import '../../game.ht'
import '../location/site.ht'
import '../../name/organization.ht'
import '../../l10n.ht'
import '../common.ht'
import '../../l10n.ht'

final kOrganizationCategories = [
  'cultivation',
  'gang',
  'religion',
  'empire',
  'business',
]

final kEmpireRankTitles = {
  '0': getLocaleString('empire.titleRank0'),
  '1': getLocaleString('empire.titleRank1'),
  '2': getLocaleString('empire.titleRank2'),
  '3': getLocaleString('empire.titleRank3'),
  '4': getLocaleString('empire.titleRank4'),
  '5': getLocaleString('empire.titleRank5'),
  '6': getLocaleString('empire.titleRank6'),
  'creator': getLocaleString('empire.titleCreator'),
  'elder': getLocaleString('empire.titleElder'),
  'disipliner': getLocaleString('empire.titleDiscipliner'),
}

final kCultivationRankTitles = {
  '0': getLocaleString('cultivation.titleRank0'),
  '1': getLocaleString('cultivation.titleRank1'),
  '2': getLocaleString('cultivation.titleRank2'),
  '3': getLocaleString('cultivation.titleRank3'),
  '4': getLocaleString('cultivation.titleRank4'),
  '5': getLocaleString('cultivation.titleRank5'),
  '6': getLocaleString('cultivation.titleRank6'),
  'creator': getLocaleString('cultivation.titleCreator'),
  'elder': getLocaleString('cultivation.titleElder'),
  'disipliner': getLocaleString('cultivation.titleDiscipliner'),
  'guest': getLocaleString('cultivation.titleGuest'),
}

final kGangRankTitles = {
  '0': getLocaleString('gang.titleRank0'),
  '1': getLocaleString('gang.titleRank1'),
  '2': getLocaleString('gang.titleRank2'),
  '3': getLocaleString('gang.titleRank3'),
  '4': getLocaleString('gang.titleRank4'),
  '5': getLocaleString('gang.titleRank5'),
  '6': getLocaleString('gang.titleRank6'),
  'counsellor': getLocaleString('gang.titleCounsellor'),
  'elder': getLocaleString('gang.titleElder'),
  'disipliner': getLocaleString('gang.titleDiscipliner'),
}

final kReligionRankTitles = {
  '0': getLocaleString('religion.titleRank0'),
  '1': getLocaleString('religion.titleRank1'),
  '2': getLocaleString('religion.titleRank2'),
  '3': getLocaleString('religion.titleRank3'),
  '4': getLocaleString('religion.titleRank4'),
  '5': getLocaleString('religion.titleRank5'),
  '6': getLocaleString('religion.titleRank6'),
  'creator': getLocaleString('religion.titleCreator'),
  'elder': getLocaleString('religion.titleElder'),
  'disipliner': getLocaleString('religion.titleDiscipliner'),
}

final kBusinessRankTitles = {
  '0': getLocaleString('business.titleRank0'),
  '1': getLocaleString('business.titleRank1'),
  '2': getLocaleString('business.titleRank2'),
  '3': getLocaleString('business.titleRank3'),
  '4': getLocaleString('business.titleRank4'),
  '5': getLocaleString('business.titleRank5'),
  '6': getLocaleString('business.titleRank6'),
  'creator': getLocaleString('business.titleCreator'),
  'craftsman': getLocaleString('business.titleCraftsman'),
  'chef': getLocaleString('business.titleChef'),
}

const kOrganizationCategoryCultivation = 'cultivation'
const kOrganizationCategoryGang = 'gang'
const kOrganizationCategoryReligion = 'religion'
const kOrganizationCategoryEmpire = 'empire'
const kOrganizationCategoryBusiness = 'business'

const kOrganizationDevelopmentMax = 5

final kOrganizationThinkings = {
  cultivation: [
    'martialArtsAddiction',
    'strengthAdmiration',
    'masterRespect',
    'mentor',
    'sectarianism',
  ],
  business: [
    'everyOneIsForHimself',
    'asLongAsProfit',
    'privateProperty',
    'spiritOfContract',
    'monopolism',
  ],
  gang: [
    'swordLover',
    'brotherhood',
    'chivalrousness',
    'promise',
    'communism',
  ],
  religion: [
    'coreligionist',
    'missionary',
    'discipline',
    'theology',
    'religious',
  ],
  empire: [],
  craftsmanship: [],
  restaurant: [],
}

const _kOrganizationThinkingsOnRecruit = 3

const _kLeaderRank = 80

struct Organization {
  construct ({
    category,
    name,
    nationId,
    headquartersId,
    leaderId,
    development = 0,
    rankTitles,
  }) {
    assert(game.locations.containsKey(headquartersId))
    assert(game.characters.containsKey(leaderId))
    assert(kOrganizationCategories.contains(category))

    this.headquartersId = headquartersId
    this.leaderId = leaderId
    this.entityType = kEntityTypeOrganization
    this.category = category

    this.index = game.organizations.length
    if (name) {
      this.name = name
    } else {
      var conflict = false
      do {
        final randomName = generateOrganizationName(category: this.category)
        this.name = randomName.name
        conflict = game.organizations.values.where((element) =>
          element.name == this.name).isNotEmpty
      } while (conflict)
    }
    this.id = 'clan_${this.index}_${this.name}'
    game.organizations[this.id] = this

    when (this.category) {
      'empire' -> {
        this.rankTitles = rankTitles ?? kEmpireRankTitles
      }
      'cultivation' -> {
        this.rankTitles = rankTitles ?? kCultivationRankTitles
      }
      'gang' -> {
        this.rankTitles = rankTitles ?? kGangRankTitles
      }
      'religion' -> {
        this.rankTitles = rankTitles ?? kReligionRankTitles
      }
      'business' -> {
        this.rankTitles = rankTitles ?? kBusinessRankTitles
      }
    }
    
    // 本门派统治的据点 id 列表
    this.locationIds = []
    // 本门派成员，key 是 角色的 id，value 是角色的 rank
    this.characterIds = {}

    // 组织规模，决定了组织所能允许的成员数量
    // 规模也决定了新成员加入组织之后的等级
    // 例如当前规模0，那么总人数限制为 5
    // 新人加入时的等级为 1
    this.development = development
    
    if (nationId) {
      assert(game.nations.containsKey(nationId))
      final nation = game.nations[nationId]
      nation.organizationIds.add(this.id)
      this.nationId = nationId
    }

    final headquarters = game.locations[headquartersId]
    final leader = game.characters[leaderId]
    // 触发创派事件，创派事件和成为掌门是两个事件
    final incidentContent = getLocaleString('incidentOrganizationCreate', [
      leader.name,
      headquarters.name,
      this.name,
    ])
    Incident(
      subjectIds: [leader.id],
      content: incidentContent,
      order: 15,
      isGlobal: true,
    )
    addCharacterToOrganization(leader, this)
    setLeader(leader, this)

    // addLocationToOrganization(location, this)
    stationOrganization(
      this,
      headquarters,
      characterIds: [leaderId],
      isHeadquarters: true,
    )
  }
}

// fun getLeaderTitle(organization) {
//   return organization.name + getLocaleString('${organization.category}LeaderTitle')
// }

// 必须已经是门派成员，才可以设定为掌门
fun setLeader(character, organization) {
  assert(character.organizationId == organization.id)
  organization.leaderId = character.id
  final titleId = '${organization.category}.titleRank0'
  final title = getLocaleString(titleId)
  character.titles[titleId] = title
  character.currentTitleId = titleId
  character.organizationRank = 0
  final incidentContent = getLocaleString('incidentLeader', [
    character.name,
    organization.name,
    title,
  ])
  Incident(
    subjectIds: [character.id],
    content: incidentContent,
    order: 15,
    isGlobal: true,
  )
}

// 类似 equip，将会在 location 上保存一个新的数据对象
// {
//   id,
//   isHeadquarters
//   category,
//   characterIds, // 指在这个地点的该组织的人员，并不一定等于该组织的全部人员
// }
fun stationOrganization(
  organization,
  location, {
  characterIds,
  isHeadquarters = false,
}) {
  assert(!location.organizations.containsKey(organization.id))
  characterIds ??= organization.characterIds.keys.toList()
  if (isHeadquarters) {
    organization.headquartersId = location.id
  }
  Site(
    category: organization.category,
    locationId: location.id,
    organizationId: organization.id,
    name: organization.name,
  ) 
  location.organizations[organization.id] = {
    id: organization.id,
    isHeadquarters: isHeadquarters,
    category: organization.category,
    characterIds: [],
  }

  for (final id in characterIds) {
    final character = game.characters[id]
    setCharacterHome(character, location, organizationName: organization.name)
  }

  organization.locationIds.add(location.id)
}

// 组织中每个等级的人数上限
// 数字越大，等级越低，0是掌门
fun maxMemberOfRank(n: int) {
  return n * n
}

// 组织可以拥有的人数上限取决于组织发展度
// 最少是 0，可以拥有 掌门 1 人，rank 1：4 人
fun maxMemberOfDevelopment(n: int) {
  var number = 0
  for (final i in range(1, n + 2)) {
    number += maxMemberOfRank(i)
  }
  return number
}

fun removeCharacterFromOrganization(character) {
  if (!character.organizationId) return

  final org = game.organizations[character.organizationId]
  org.characterIds.remove(character.id)
  delete character.organizationId
}

fun organizationMemberReachMax(organization) {
  return organization.characterIds.length >= maxMemberOfDevelopment(organization.development)
}

// 新加入的成员的等级，是当前的组织发展度 + 1
fun addCharacterToOrganization(character, organization, { incidentOccured = true }) {
  assert(!organization.characterIds.contains(character.id))
  if (organizationMemberReachMax(organization)) {
    engine.warning('组织 ${organization.name} 的成员数量达到上限，在提升发展度之前无法再添加新成员。')
    return false
  }
  if (character.organizationId != null) {
    assert(character.organizationId != organization.id)
    removeCharacterFromOrganization(character)
  }
  character.organizationRank = organization.development + 1
  organization.characterIds[character.id] = character.organizationRank
  character.organizationId = organization.id

  if (incidentOccured) {
    final incidentContent = getLocaleString('incidentEnlist', [
      character.name,
      organization.name,
    ])
    Incident(
      subjectIds: [character.id],
      content: incidentContent,
      order: 10,
      isPrivate: false,
    )
  }

  final location = game.locations[organization.headquartersId]
  setCharacterHome(character, location, organizationName: organization.name)
  final r = random.nextInt(_kOrganizationThinkingsOnRecruit)
  for (final i in range(r)) {
    var thinking
    do {
      thinking = random.nextIterable(kOrganizationThinkings[organization.category])
    } while (character.thinkings.contains(thinking))
    character.thinkings.add(thinking)
  }

  setCharacterLocationId(character, organization.headquartersId)
  return true
}

fun addOrganizations(orgs: List) {
  engine.info('载入组织数据')
  for (final org in orgs) {
    game.organizations[org.id] = org
  }
}

fun addOrganization(char) {
  game.organizations[char.id] = char
}

fun getOrganizationById(id: str) {
  return game.organizations[id]
}

fun getOrganizations() {
  return game.organizations
}

fun increaseOrganizationDevelopment(organization) {
  if (organization.development < kOrganizationDevelopmentMax) {
    ++organization.development
  } else {
    engine.warning('组织 ${organization.name} 的发展度（${organization.development}）已无法再增加。')
  }
}
