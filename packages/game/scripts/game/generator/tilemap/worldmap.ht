import '../random_names/random_names.ht' as randomNames
import '../../game.ht'
import '../../location.ht'
import '../../character.ht'
import '../../nation.ht'
import 'hexagonal_tile.ht'
import 'component.ht'
import {
  tilePos2Index,
  getMapTileNeighbors,
  calculateRoute } from '../../map.ht'

/// 生成世界地图的算法
///
/// spritesheet上不同地形对应的编号：
/// 0：虚空
/// 1：浅海
/// 2：深海
/// 3：陆地（草原）
///
/// 算法思路：
///
/// 用海面填满整个地图空间
///
/// 找到一个出发点，通常在地图中间部分
///
/// 找到出发点周围的方块，将其加入一个数组，作为当前陆地的边界
///
/// 将当前方块变成陆地，检查陆地比例，如果不够，就从当前陆地边界中随机挑选一个方块，将其变成陆地，同时重新计算边界。
///
/// 循环上述步骤，直到陆地和海水的比例达到预设值
///
/// 为陆地所属的整个大地块命名，然后找到尚未命名的地块，将所有相连的地块作为一个整体命名，直到所有的地块都有名字，根据地块大小有不同命名方式
///
/// 根据城市密度，生成城市，并根据城市位于陆地、海边还是海中，对其进行特殊命名
///
/// 对于每个城市，链接最近的另外两个城市，使用 A* 寻路算法，标记经过的地块
///
/// 对于没有道路经过的地块，随机填充山脉和树林

const _defaultWorldWidth = 12
const _defaultWorldHeight = 12
const _defaultNumberOfNation = 5
const _kSpriteWater = 1
const _kSpriteShelf = 2
const _kSpriteLand = 3
const _kSpriteCity = 4
const _kSpriteForest = 5
const _kSpriteMountain = 6
const _kLocationCategoryCity = 'city'
const _kLocationCategoryUnchartedLand = 'unchartedLand'
const _kLocationCategoryUnchartedIsland = 'unchartedIsland'
const _kZoneVoid = 'empty'
const _kZoneWater = 'water'
const _kZoneContinent = 'continent'
const _kZoneIsland = 'island'
const _kZoneLake = 'lake'
const _kMinZoneSizeForLocation = 9 // 最大据点密度
const _kMaxZoneSizeForLocation = 18 // 最小据点密度
const _kMinZoneSizeForSea = 8
const _kZoneSizeForHarbor = 18
const _kForestThreshold = 0.5
const _kMountainThreshold = 0.2
const _kMinLocationNumberForOrganization = 0.5 // 最大组织密度
const _kMaxLocationNumberForOrganization = 8 // 最小组织密度
const _kMinNpcNumberForOrganization = 3
const _kMaxNpcNumberForOrganization = 18

struct Zone {
  var index: int
  var name: str
  var category: str
  var count = 0
  final terrainIndexes = Set()

  construct ({name, category}) {
    this.index = game.world.zones.length
    this.name = name
    this.category = category
    game.world.zones.add(this)
  }
}

fun addTerrain2Zone(zoneIndex, terrain) {
  assert(0 <= zoneIndex && zoneIndex < game.world.zones.length)
  if (terrain.zoneIndex != null && terrain.zoneIndex >= 0) {
    final oldZone = game.world.zones[terrain.zoneIndex]
    oldZone.terrainIndexes.remove(terrain.index)
  }
  final zone = game.world.zones[zoneIndex]
  terrain.zoneIndex = zoneIndex
  terrain.zoneCategory = zone.category
  zone.terrainIndexes.add(terrain.index)
}

fun createWorldMap({
    terrainSpriteSheet: str,
    tileShape = 'hexagonalVertical',
    width = _defaultWorldWidth,
    height = _defaultWorldHeight,
    style = 'island',
    landProportion = 0.55,
    gridWidth = 32.0,
    gridHeight = 28.0,
    tileSpriteSrcWidth = 32.0,
    tileSpriteSrcHeight = 48.0,
    tileOffsetX = 0.0,
    tileOffsetY = 2.0,
  }) {
  
  game.nations = {}
  game.characters = {}
  game.locations = {}
  game.organizations = {}
  game.mazes = {}

  game.world = {
    terrainSpriteSheet,
    tapSelect: true,
    tileShape,
    width,
    height,
    gridWidth,
    gridHeight,
    tileSpriteSrcWidth,
    tileSpriteSrcHeight,
    tileOffsetX,
    tileOffsetY,
    zones: [],
    terrains: [],
  }

  for (var j in range(height)) {
    for (var i in range(width)) {
      game.world.terrains.add(
        {
          ...Tile(i + 1, j + 1),
          index: tilePos2Index(i + 1, j + 1),
          spriteIndex: _kSpriteWater,
          overlaySprite: {},
        }
      )
    }
  }

  // 没有区域，也是一个区域
  final voidZone = Zone(name: '无', category: _kZoneVoid)

  // 当前地域的边缘格子坐标
  final shelf = Set()

  // 刷新当前地域的边缘格子坐标
  fun updateShelf(left, top) {
    shelf.remove(tilePos2Index(left, top))
    final neighbors = getMapTileNeighbors(left, top, restricted2Zone: false)
    for (final tile in neighbors) {
      if (tile.spriteIndex == _kSpriteWater) {
        shelf.add(tile.index)
      }
    }
  }

  // 生成地域
  final landArea = (width * height * landProportion).floor()

  final minLeft = width ~/ 3
  final minTop = height ~/ 3
  final maxLeft = width ~/ 3 * 2
  final maxTop = height ~/ 3 * 2

  final startLeft = Math.randomInt(maxLeft - minLeft) + minLeft
  final startTop = Math.randomInt(maxTop - minTop) + minTop
  game.world.heroX = startLeft
  game.world.heroY = startTop
  // print('start: ${startLeft},${startTop}')
  final startIndex = tilePos2Index(startLeft, startTop)
  final firstTile = game.world.terrains[startIndex]
  firstTile.spriteIndex = _kSpriteLand
  final landZone = Zone(category: _kZoneContinent)
  addTerrain2Zone(landZone.index, firstTile)
  updateShelf(startLeft, startTop)

  var currentLandSize = 1
  while (currentLandSize < landArea) {
    // print('expanding land')
    ++currentLandSize
    final tile = game.world.terrains[shelf.random]
    updateShelf(tile.left, tile.top)
    tile.spriteIndex = _kSpriteLand
    addTerrain2Zone(landZone.index, tile)
  }

  // 生成地图上的不同地域
  // 根据相邻的格子的地域，分配地域索引
  fun updateZone(tile, [spriteIndex, unzonedNeighbors]) {
    assert(tile.zoneIndex == null)
    var willCreateZone = true
    if (spriteIndex != null) {
      willCreateZone = false
    } else {
      spriteIndex = tile.spriteIndex
    }
    unzonedNeighbors ??= {}
    // print('processing: ${tile.left},${tile.top}, spriteIndex: ${tile.spriteIndex}')
    tile.zoneIndex = -1 // mark this tile is being processed, will skip it when process others
    final neighbors = getMapTileNeighbors(tile.left, tile.top)
    var foundAdjacentZone = false
    for (final neighbor in neighbors) {
      // skip tiles that has diffrent spriteIndex and those still being processed
      if (neighbor.spriteIndex != spriteIndex) continue
      if (neighbor.zoneIndex == null) {
        updateZone(neighbor, spriteIndex, unzonedNeighbors)
      }
      if (neighbor.zoneIndex == -1) {
        unzonedNeighbors[neighbor.index] = neighbor
        continue
      }
      addTerrain2Zone(neighbor.zoneIndex, tile)
      foundAdjacentZone = true
      // print('Found neighbor: ${neighbor.left},${neighbor.top}, zoneIndex: ${neighbor.zoneIndex}')
      break
    }
    if (!foundAdjacentZone && willCreateZone) {
      var category = tile.spriteIndex == _kSpriteLand ? _kZoneContinent : _kZoneWater
      final zone = Zone(category: category)
      // print('Created zone! left: ${tile.left}, top: ${tile.top} spriteIndex: ${tile.spriteIndex}, zoneIndex: ${zone.index}')
      addTerrain2Zone(zone.index, tile)
      // print('processing previoused stored unzoned tiles: ${unzonedNeighbors}')
      for (final unzonedTile of unzonedNeighbors) {
        if (unzonedTile.zoneIndex == -1) {
          addTerrain2Zone(zone.index, unzonedTile)
        }
      }
    }
  }

  var unzonedTile

  while (
    unzonedTile = game.world.terrains.firstWhere((tile) => tile.zoneIndex == null)
  ) {
    // print('updating unzoned tile')
    updateZone(unzonedTile)
  }

  fun find2NearestLocation(origin, {restricted2Zone: bool = true}) {
    assert(game.locations.length > 0)
    var nearest
    var nearestDistance
    var secondNearest
    var secondNearestDistance
    for (final location of game.locations) {
      if (location.index == origin.index) {
        continue
      } else {
        final distance = getTileDistance(origin, location)
        if (!restricted2Zone || location.zoneIndex == origin.zoneIndex) {
          if (nearestDistance == null || distance < nearestDistance) {
            nearestDistance = distance
            nearest = location
          } else if (secondNearestDistance == null || distance < secondNearestDistance) {
            secondNearestDistance = distance
            secondNearest = location
          } else {
            continue
          }
        }
      }
    }
    return { nearest, secondNearest }
  }

  final locationIndexes = Set()

  final islands = []
  // 根据 tile 的所在位置的类型，添加属性
  fun checkPerimeterOfLocation(tile) {
    if (tile.spriteIndex == _kSpriteLand) return
    final neighbors = getMapTileNeighbors(tile.left, tile.top)
    for (final neighbor in neighbors) {
      if (neighbor.spriteIndex == _kSpriteLand) {
        tile.isHarbor = true
        tile.harborOfZoneIndex = tile.zoneIndex
        addTerrain2Zone(neighbor.zoneIndex, tile)
        return
      }
    }
    islands.add(tile)
  }

  // 生成据点
  for (final zone in game.world.zones) {
    if (zone.terrainIndexes.length < _kMinZoneSizeForLocation) continue
    // print('processing zone: ${zone.name} size: ${zone.terrainIndexes.length}')
    final minLocNum = zone.terrainIndexes.length ~/ _kMaxZoneSizeForLocation
    final maxLocNum = zone.terrainIndexes.length ~/ _kMinZoneSizeForLocation
    final locationNumber = Math.randomInt(maxLocNum - minLocNum) + minLocNum
    final zoneLocationIndexes = Set()
    while (zoneLocationIndexes.length < locationNumber) {
      var index
      do {
        // print('generating location index')
        index = zone.terrainIndexes.random
      } while (locationIndexes.contains(index))
      final tile = game.world.terrains[index]
      // location category只有两种：城市和秘境
      final location = Location(terrainIndex: tile.index, category: _kLocationCategoryCity)
      zoneLocationIndexes.add(index)
      locationIndexes.add(index)
      checkPerimeterOfLocation(tile)
      tile.locationId = location.id
      tile.overlaySprite.index = _kSpriteCity
    }
  }

  // 每个岛屿是独立的 zone
  for (final island in islands) {
    final islandZone = Zone(category: _kZoneIsland)
    addTerrain2Zone(islandZone.index, island)
  }

  // 因为 zone 的命名和其大小有关，因此在最后单独处理
  for (final zone in game.world.zones) {
    if (zone.name) {
      continue
    }
    late names
    late category
    if (zone.category == _kZoneWater) {
      category = zone.terrainIndexes.length < _kMinZoneSizeForSea ? _kZoneLake : _kZoneWater
    } else {
      category = zone.category
    }
    names = randomNames.getZone(1, category: category)
    zone.name = names.first.name
  }
  
  // 将大陆和海洋接触的地方填充为大陆架
  for (final index in shelf) {
    final tile = game.world.terrains[index]
    tile.spriteIndex = _kSpriteShelf
  }

  for (final location of game.locations) {
    final start = game.world.terrains[location.terrainIndex]
    // 确保陆地上的每个城市至少可以链接到另外两个城市
    if (start.zoneCategory == _kZoneContinent) {
      final searchResult = find2NearestLocation(location, restricted2Zone: true)
      if (searchResult.nearest) {
        final end = game.world.terrains[searchResult.nearest.terrainIndex]
        final route = calculateRoute(start, end)
        if (route) {
          // routes.add(route)
          for (final index in route) {
            final terrain = game.world.terrains[index]
            terrain.isRoute = true
          }
        }
      }
      if (searchResult.secondNearest) {
        final end = game.world.terrains[searchResult.secondNearest.terrainIndex]
        final route = calculateRoute(start, end)
        if (route) {
          // routes.add(route)
          for (final index in route) {
            final terrain = game.world.terrains[index]
            terrain.isRoute = true
          }
        }
      }
    }
  }

  // 将大陆上没有道路的方格，随机填充为平原、森林、山地
  for (final index in landZone.terrainIndexes) {
    final terrain = game.world.terrains[index]
    if (!terrain.isRoute) {
      final r = Math.random()
      if (r < _kMountainThreshold) {
        terrain.spriteIndex = _kSpriteMountain
      } else if (r < _kForestThreshold) {
        terrain.spriteIndex = _kSpriteForest
      }
    }
  }

  // print('generating nations')
  // 随机挑选几个城市，作为各个国家的都城
  while (game.nations.length < _defaultNumberOfNation) {
    final nation = Nation(capitalId: game.locations.keys.random)
    final location = game.locations[nation.capitalId]
    final terrain = game.world.terrains[location.terrainIndex]
    addTerrain2Nation(nation.id, terrain)
  }
  // print(game.nations)

  // 生成地图上的不同国家
  // 根据相邻的格子的 nationId 进行分配
  fun expandNation(nation) {
    // print('processing nation: ${nation}')
    assert(nation.borderIndexes != null && nation.borderIndexes.length > 0)
    var expanded = false
    // avoid editing iterable while iterating
    final currentBorder = nation.borderIndexes.toList()
    for (final index in currentBorder) {
      final terrain = game.world.terrains[index]
      if (terrain.nationId == null && terrain.zoneCategory == _kZoneContinent) {
        addTerrain2Nation(nation.id, terrain)
        expanded = true
      }
    }
    return expanded
  }

  // 随机选择各个国家的都城，然后各自扩散都城直到没有没有分配国家的区块
  var unoccupiedTile
  while (
    unoccupiedTile = game.world.terrains.firstWhere((tile) => tile.nationId == null && tile.zoneCategory == _kZoneContinent)
  ) {
    final nation = game.nations.values.random
    expandNation(nation)
  }

  final nationColors = Map()
  for (final nation of game.nations) {
    nationColors[nation.id] = nation.color
  }
  engine.updateNationColors(nationColors)

  // 向世界中填充NPC
  var minOrgNum = game.locations.length ~/ _kMaxLocationNumberForOrganization
  final maxOrgNum = game.locations.length ~/ _kMinLocationNumberForOrganization
  if (minOrgNum < 2) {
    minOrgNum = 2
  }
  if (maxOrgNum < minOrgNum) {
    maxOrgNum = minOrgNum
  }
  final orgNum = Math.randomInt(maxOrgNum - minOrgNum) + minOrgNum
  
  for (var i in range(orgNum)) {
    // 在这个据点生成组织相关的场景，总堂，士宅等等。并根据组织类型，生成一些功能性建筑
    final org = Organization()
    final loc = game.world.terrains[locationIndexes.random]
    org.headquarterLocationId = loc.id
    loc.organizationId ??= org.id
    // loc.scenes.add

    final npcCount = Math.randomInt(_kMaxNpcNumberForOrganization - _kMinNpcNumberForOrganization) + _kMinNpcNumberForOrganization
    final leader = Character()
    leader.organizationId = org.id
    for (var i in range(npcCount - 1)) {
      final char = Character()
      char.organizationId = org.id
    }
  }
  
  final playerCharacter001 = {
    id: 'playerCharacter001',
    name: '玲', // 用户创建的人物，名字是固定的而不是来自语言包
    avatar: 'avatar/female/29.jpg',

    // 组织

    // 效忠
    allegiance: {
      id: 'xuanqingzi',
      loyalty: 100,
    },
    
    fame: 0,
    infamy: 0,
    looks: 75,

    currentLife: 100, // 生命
    life: 100,
    currentSpirit: 100, // 精血
    spirit: 100,
    currentsStamina: 100, // 元气
    stamina: 100,

    strength: 65, // 力量
    dexterity: 78, // 灵巧
    perception: 46, // 感知
    intelligence: 89, // 悟性
    memory: 66, // 记忆

    // 境界
    spiritRank: 0,
    // 五行灵根
    spiritRoots: {
      water: 10,
      wood: 5,
      earth: 5,
      metal: 5,
      fire: 5,
    },
    
    currentLocation: 'baiheguan',
    currentScene: null,

    talisman: [
      {
        index: 1,
        name: 'talisman.tier0.steelSword',
        attack: 25,
        speed: 15,

        skill: {
          name: 'talisman.steelSword.skill',
          attack: 100,
          speed: 25,
        }
      },
    ],
  }
  addCharacter(playerCharacter001)
  setCurrentCharacter('playerCharacter001')

  return MapComponent.fromJson(game.world.toJson())
}
