import 'game.ht'
import 'l10n.ht'

const kTicksPerDay = 4 //每天的回合数 morning, afternoon, evening, night
const kDaysPerWeek = 10 //每旬的天数
const kTicksPerWeek = kDaysPerWeek * kTicksPerDay //每旬的回合数 40
const kDaysPerMonth = 30 //每月的天数
const kTicksPerMonth = kDaysPerMonth * kTicksPerDay //每月的回合数 120
const kMonthsPerYear = 12 //每年的月数
const kTicksPerYear = kMonthsPerYear * kTicksPerMonth //每年的回合数 1440

fun toYear(timestamp) => (timestamp ~/ kTicksPerYear)
fun toMonth(timestamp) => (timestamp % kTicksPerYear) ~/ kTicksPerMonth
fun toDay(timestamp) => (timestamp % kTicksPerMonth) ~/ kTicksPerDay

fun getCurrentYear => toYear(game.timestamp)
fun getCurrentMonth => toMonth(game.timestamp)
fun getCurrentDay => toDay(game.timestamp)

final kHourString = {
  '0': 'morning',
  '1': 'afternoon',
  '2': 'evening',
  '3': 'night',
}

fun getCurrentDateTimeString() {
  final d = formatDateTimeString(game.timestamp, format: 'date')
  final h = (game.timestamp % 4).toString()
  final h2 = getLocaleString(kHourString[h])
  return '${d}${h2}'
}

// [format] = 'age' | 'date' | 'time' . 'ymd' | 'ym' | 'md' | 'd' | 'auto'
fun formatDateTimeString(timestamp: int, {format: str}) {
  var yearN = toYear(timestamp)
  var monthN = toMonth(timestamp)
  var dayN = toDay(timestamp)
  
  var yearC
  var monthC
  var dayC

  final t = format?.split('.').first ?? 'date'
  if (t == 'age') {
    yearC = getLocaleString('ageYear')
    monthC = getLocaleString('ageMonth')
    dayC = getLocaleString('ageDay')
  } else if (t == 'date') {
    yearC = getLocaleString('dateYear')
    monthC = getLocaleString('dateMonth')
    dayC = getLocaleString('dateDay')
    ++yearN
    ++monthN
    ++dayN
  } else {
    yearC = getLocaleString('timeYear')
    monthC = getLocaleString('timeMonth')
    dayC = getLocaleString('timeDay')
  }

  final year = '${yearN}${yearC}'
  final month = '${monthN}${monthC}'
  final day = '${dayN}${dayC}'

  final fmt = format?.split('.').last
  when (fmt) {
    'y'-> {
      return year
    }
    'm'-> {
      return month
    }
    'd'-> {
      return day
    }
    'ym'-> {
      return '${year}${month}'
    }
    'md'-> {
      return '${month}${day}'
    }
    'ymd'-> {
      return '${year}${month}${day}'
    }
    else-> {
      if (fmt == 'age') {
        if (yearN == 0) {
          if (monthN == 0) {
            return day
          } else {
            return month
          }
        } else {
          return year
        }
      } else {
        return '${year}${month}${day}'
      }
    }
  }
}

fun toDateString(timestamp) => formatDateTimeString(timestamp)

fun toTimeString(timestamp) => formatDateTimeString(timestamp, format: 'time.ymd')

fun toAgeString(timestamp) => formatDateTimeString(timestamp, format: 'age')

fun getEntityAgeString(entity) {
  return toAgeString(game.timestamp - entity.birthTimestamp)
}

fun getEntityBirthDateString(entity) {
  return formatDateTimeString(game.timestamp - entity.birthTimestamp, format: 'date.md')
}