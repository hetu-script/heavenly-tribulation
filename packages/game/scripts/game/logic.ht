import 'game.ht'
import 'event.ht'
import 'entity/character/character.ht'
import 'l10n.ht'
import 'maze/maze.ht'
import 'quest/quest.ht'
import 'name/name.ht' as nameGenerator
import 'tile/common.ht'

const kLocationQuestProbability = 0.35

fun getQuestMaxFromLocationDevelopment(development) -> int {
  if (development > 20) {
    return 5
  } else {
    return development ~/ 4 + 1
  }
}

fun updateGame({nextTick = true}) {
  // 每个tick执行的代码
  handleBabies()

  // 每个月执行的代码
  if (game.timestamp % kTicksPerMonth == 0) {
    generateQuests()
  }

  onGameEvent('onUpdate')

  if (nextTick) ++game.timestamp
}

// 每个tick检查一次当前是否有婴儿出生
fun handleBabies {
  final bornIds = Set()
  for (final baby of game.babies) {
    if (game.timestamp - baby.conceptionTimestamp < baby.pregnancyTime) continue
    final mother = game.characters[baby.motherId]
    mother.isPregnant = false
    final location = game.locations[mother.locationId]
    final character = Character(
      familyName: baby.familyName,
      shorname: baby.shorname,
      isFemale: baby.isFemale,
      birthTimestamp: game.timestamp,
      locationId: location.id,
      isNewborn: true,
      fatherId: baby.fatherId,
      motherId: baby.motherId,
    )
    bornIds.add(baby.id)
  }
  for (final id in bornIds) {
    delete game.babies[id]
  }
}

// 每个月生成一次每个地点的悬赏任务
fun generateQuests {
  for (final location of game.locations) {
    final questMax = getQuestMaxFromLocationDevelopment(location.development)
    if (location.quests.length >= questMax) continue
    if (random.nextDouble() > kLocationQuestProbability) continue

    // 生成盗贼副本类悬赏任务，只在有国家的据点上生成
    if (!location.nationId) continue
    final nation = game.nations[location.nationId]
    if ((random.nextDouble() < location.stability / 100) && !nation.hasBanditEvent && !location.hasBanditEvent) {

      var mazeTerrain
      for (final terrainIndex in nation.territoryIndexes) {
        final terrain = game.world.terrains[terrainIndex]
        if (terrain.locationId) continue
        if (terrain.spriteIndex == kSpriteLand) {
          mazeTerrain = terrain
          break
        }
      }

      if (mazeTerrain) {
        nation.hasBanditEvent = true
        location.hasBanditEvent = true

        final bossName = nameGenerator.getCharacter(1).first.name
        final quest = QuestBanditCamp(
          locationId: location.id,
          mazeTerrainIndex: mazeTerrain.index,
          nationId: nation.id,
          kind: kQuestKindKillBanditBoss,
          bossName: bossName,
        )
        engine.info('在 ${location.name} 生成悬赏任务：${quest.id}，副本地点：${mazeTerrain.left}, ${mazeTerrain.top}')
        location.quests[quest.id] = quest
      }
    }
  }
}

fun onGameEvent(id: str, [arg]) {
  for (final handler of eventHandlers) {
    final callback = handler[id];
    if(callback is function) {
      callback(arg)
    }
  }
}
