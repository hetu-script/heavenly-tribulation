import 'character/family.json' as familyNames
import 'character/female.json' as femaleNames
import 'character/male.json' as maleNames
import 'character/middle.json' as middleNames
import '../random.ht'

fun generateCharacterName({isFemale, familyName, middleCharacter}) {
  var theFamilyName
  if (!familyName) {
    theFamilyName = random.nextIterable(familyNames)
  } else {
    theFamilyName = familyName
  }
  final f = isFemale ?? random.nextBool()
  final namesOfASex = f ? femaleNames : maleNames
  final r = random.nextDouble()
  var name = ''
  // 单字的名字
  if (r < 0.33333333) {
    if (middleCharacter) {
      name = middleCharacter
    } else {
      name = random.nextIterable(namesOfASex)
    }
  }
  // 两个实字
  else if (r < 0.66666666) {
    final theMiddleCharacter
    if (middleCharacter) {
      theMiddleCharacter = middleCharacter
    } else {
      theMiddleCharacter = random.nextIterable(namesOfASex)
    }
    final theLastCharacter = random.nextIterable(namesOfASex)
    name = theMiddleCharacter + theLastCharacter
  }
  // 中间是虚字
  else {
    final theMiddleCharacter
    if (middleCharacter) {
      theMiddleCharacter = middleCharacter
    } else {
      theMiddleCharacter = random.nextIterable(middleNames)
    }
    final theLastCharacter = random.nextIterable(namesOfASex)
    name = theMiddleCharacter + theLastCharacter
  }
  return {
    familyName: theFamilyName,
    shortName: name,
    name: theFamilyName + name,
    isFemale: f,
  }
}
