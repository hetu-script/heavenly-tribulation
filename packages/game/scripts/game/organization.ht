import 'game.ht'
import 'generator/random_names/random_names.ht' as randomNames

struct Organization {
  construct ({
    category,
    name,
    nationId,
    headquartersId,
    leaderId,
    development,
  }) {
    assert(nationId != null)
    assert(headquartersId != null)
    assert(leaderId != null)

    this.index = game.organizations.length
    if (name) {
      this.name = name
    } else {
      final names = randomNames.getOrganization(1, category: category)
      this.name = names.first.name
    }
    this.id = 'Clan_${this.index}_${this.name}'
    
    // 本门派统治的据点 id 列表
    this.locationIds = Set()
    // 本门派成员 id 列表
    this.characterIds = Set()
    
    this.nationId = nationId
    final nation = game.nations[nationId]
    nation.organizationIds.add(this.id)

    this.headquartersId = headquartersId
    final location = game.locations[headquartersId]
    addLocation2Organization(this, location)

    final leader = game.characters[leaderId]
    addCharacter2Organization(this, leader)
    setOrganizationLeader(this, leader)

    // 组织规模，组织所能允许的等级数量 = 规模
    this.development = development ?? 1

    game.organizations[this.id] = this
  }
}

// 必须已经是门派成员，才可以设定为掌门
fun setOrganizationLeader(organization, character) {
  assert(character.organizationId == organization.id)
  organization.leaderId = character.id
  character.titles[kOrganizationLeaderTitle] = getOrganizationLeaderTitle(organization)
  character.currentTitleId = kOrganizationLeaderTitle
}

// 组织中每个等级的人数上限
// 数字越大，等级越低，0是掌门
fun maxMemberNumberOfOrganizationRank(n: int) {
  return n * n
}

// 组织发展度，等于组织可以拥有的境界上限
// 最少是 1，可以拥有 筑基 1 人，炼气 4 人，外门 9 人
// 最多是 10
fun maxMemberNumberOfOrganizationDevelopment(n: int) {
  assert(n > 0)
  var number = 0
  for (var i in range(n + 1)) {
    number += maxMemberNumberOfOrganizationRank(i)
  }
  return number
}

fun removeCharacterFromOrganization(character) {
  if (!character.organizationId) return

  final org = game.organizations[character.organizationId]
  org.characterIds.remove(character.id)
  character.organizationId = null
}

fun addCharacter2Organization(organization, character) {
  assert(!organization.characterIds.contains(character.id))
  if (character.organizationId != null) {
    assert(character.organizationId != organization.id)
    removeCharacterFromOrganization(character)
  }
  organization.characterIds.add(character.id)
  character.organizationId = organization.id
}

fun removeLocationFromOrganization(location) {
  if (!terrain.organizationId) return

  final org = game.organizations[terrain.organizationId]
  org.locationIds.remove(location.id)
  location.organizationId = null
}

fun addLocation2Organization(organization, location) {
  if (location.organizationId != null) {
    assert(location.organizationId != organization.id)
    removeLocationFromOrganization(location)
  }
  // 只能向本国的门派分配本国统治的据点
  assert(location.nationId == organization.nationId)
  location.organizationId = organization.id
  assert(!organization.locationIds.contains(location.id))
  organization.locationIds.add(location.id)
}

fun addOrganizations(orgs: List) {
  if (game.debug) {
    print('河图: 载入组织数据 ...')
  }
  for (final org in orgs) {
    game.organizations[org.id] = org
  }
}

fun setCurrentOrganization(id: str) {
  game.hero.clanId = id;
}

fun getCurrentOrganization() {
  return getOrganizationById(game.hero.ClanId)
}

fun addOrganization(char) {
  game.organizations[char.id] = char
}

fun getOrganizationById(id: str) {
  return game.organizations[id]
}

fun getOrganizations() {
  return game.organizations
}
