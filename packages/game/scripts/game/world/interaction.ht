import '../game.ht'
import '../l10n.ht'
import '../tile/hexagonal_tile.ht'
import '../tile/tilemap.ht'
import '../logic.ht'
import '../datetime.ht'
import '../entity/item/consumable/consumable.ht'
import '../entity/item/material/material.ht'
import '../entity/item/material/jade.ht'
import '../entity/character/battle_entity.ht'
import '../binding/worldmap.ht'
import '../tile/common.ht'
import '../entity/character/enemy/bandit.ht'
import '../entity/character/creature/beast.ht'

const kFoundBanditCampProbability = 0.25
const kFoundArcanaMirageProbability = 0.05

// 打猎会增加遇到野兽的概率200%
// 这个概率将会和地点本身的野兽概率相乘
const kHuntBeastProbability = 2.0

// 山峰上可能遇到强盗和野兽，但产出也较高
const kMountainBeastProbability = 0.15
const kMountainFruitProbability = 0.4
const kMountainWoodProbability = 0.3
const kMountainOreProbability = 0.15
const kMountainJadeProbability = 0.07
const kMountainHerbProbability = 0.2

// 森林中可能遇到强盗和野兽，但产出也较高
const kForestBeastProbability = 0.07
const kForestFruitProbability = 0.8
const kForestWoodProbability = 0.6
const kForestOreProbability = 0.04
const kForestJadeProbability = 0.02
const kForestHerbProbability = 0.4

// 平原上几乎不会遇到强盗和野兽，但产出也较低
const kPlainBeastProbability = 0.02
const kPlainFruitProbability = 0.3
const kPlainWoodProbability = 0.1
const kPlainOreProbability = 0.01
const kPlainJadeProbability = 0.001
const kPlainHerbProbability = 0.1

// 湖泊上没有风暴，但产出也较低
const kLakeFishProbability = 0.25

// 海洋上可能会遇到暴风雨，但产出也较高
const kSeaStormProbability = 0.3
const kSeaFishProbability = 0.45

// 在某个地形块上进行互动操作
// 山峰或树林产出物品更多，平原产出数量较少
// 但前者会有几率碰到野兽或者强盗
fun handleWorldTerrainInteraction(left, top) {
  final hero = getHero()
  
  // var isInjured = false
  // for (final status of hero.statusEffects) {
  //   if (status.category == kStatusCategoryInjury)
  //   isInjured = true
  // }
  // if (isInjured) {
  //   showDialogByLocaleKeys(['cannotInteractWhenInjured'], character: hero)
  //   return
  // }

  // 本月剩余的天数
  final currentDayOfMonth = getCurrentDay()
  if (currentDayOfMonth > 29) {
    showDialogByLocaleKeys(['cannotInteractAtEndOfMonth'], character: hero)
    return
  }
  final restDaysOfMonth = kDaysPerMonth - currentDayOfMonth

  // 英雄剩余的体力
  // 采集消耗体力为每天4点（每个tick 1点）。
  final staminaAvailableDays = hero.stats.stamina ~/ kTicksPerDay
  if (staminaAvailableDays < 1) {
    showDialogByLocaleKeys(['notEnoughStamina'], character: hero)
    return
  }

  // 可用的探索天数
  final availableDays = Math.min(restDaysOfMonth, staminaAvailableDays)

  final terrainIndex = tilePos2Index(left, top, game.world.width)
  final terrain = game.world.terrains[terrainIndex]

  final selections = [
    'search',
  ]

  when (terrain.kind) {
    kTerrainKindMountain, kTerrainKindForest, kTerrainKindPlain -> {
      selections.add('gather')
      selections.add('woodcut')
      selections.add('mine')
      selections.add('hunt')
    }
    kTerrainKindLake, kTerrainKindSea -> {
      selections.add('fish')
    }
  }

  selections.add('cancel')
  
  showSelectionByLocaleKeys(selections).then((key) {
    when (key) {
      'search' -> {
        final entityId = terrain.entityId
        if (terrain.hasExplored) {
          handleWorldMapEntityInteraction(entityId)
        } else {
          showIntInput(getLocaleString('timeDays'), availableDays).then((days) {
            showProgress(getLocaleString('search')).then((value) {
              var i = 0
              if (entityId) {
                var found = false
                while (i < days) {
                  ++i
                  // TODO: 角色感知可以提高成功率
                  if (random.nextDouble() < kFoundBanditCampProbability) {
                    found = true
                    break
                  }
                }
                final ticks = kTicksPerDay * i
                updateGame(ticks: ticks)
                characterCostStamina(hero, ticks)

                if (found) {
                  terrain.hasExplored = true
                  handleWorldMapEntityInteraction(entityId)
                } else {
                  showDialogByLocaleKeys(['interactionGotNothing'], character: hero)
                }
              } else {
                final ticks = kTicksPerDay * i
                updateGame(ticks: ticks)
                characterCostStamina(hero, ticks)
                showDialogByLocaleKeys(['interactionGotNothing'], character: hero)
              }
            })
          })
        }
      }
      // 采集，可能会获得：药材（材料）、水果（材料）
      'gather' -> {
        showIntInput(getLocaleString('timeDays'), availableDays).then((days) {
          showProgress(getLocaleString('gather')).then((value) {
            final lootBox = { inventory: {} }
            var metBeast = false
            var i = 0
            while(i < days) {
              ++i
              updateGame(ticks: kTicksPerDay)
              characterCostStamina(hero, kTicksPerDay)
              when (terrain.kind) {
                kTerrainKindMountain -> {
                  if (random.nextDouble() < kMountainBeastProbability) {
                    metBeast = true
                    break
                  }
                  if (random.nextDouble() < kMountainHerbProbability) {
                    entityAcquireItem(lootBox, Material.herb(), incidentOccured: false)
                  }
                  if (random.nextDouble() < kMountainFruitProbability) {
                    entityAcquireItem(lootBox, Material.fruit(), incidentOccured: false)
                  }
                }
                kTerrainKindForest -> {
                  if (random.nextDouble() < kForestBeastProbability) {
                    metBeast = true
                    break
                  }
                  if (random.nextDouble() < kForestHerbProbability) {
                    entityAcquireItem(lootBox, Material.herb(), incidentOccured: false)
                  }
                  if (random.nextDouble() < kForestFruitProbability) {
                    entityAcquireItem(lootBox, Material.fruit(), incidentOccured: false)
                  }
                }
                kTerrainKindPlain -> {
                  if (random.nextDouble() < kPlainHerbProbability) {
                    entityAcquireItem(lootBox, Material.herb(), incidentOccured: false)
                  }
                  if (random.nextDouble() < kPlainFruitProbability) {
                    entityAcquireItem(lootBox, Material.fruit(), incidentOccured: false)
                  }
                }
              }
            }
            final incidentContent = getLocaleString('characterExplore', [
              hero.name,
              '${terrain.left}, ${terrain.top}',
              i,
            ])
            Incident(
              subjectIds: [hero.id],
              content: incidentContent,
              isPrivate: true,
            )
            () async {
              if (lootBox.inventory.isEmpty) {
                return showDialogByLocaleKeys(['interactionGotNothing'], character: hero)
              } else {
                characterLoot(hero, lootBox, takeAll: true)
              }
            } ().then((value) {
              if (metBeast) {
                final beast = Beast()
                final message = getLocaleString('metBeast', [beast.name])
                showDialogByStrings([message]).then((value) {
                  heroBattle(beast)
                })
              }
            })
          })
        })
      }
      // 伐木，可能会获得：木料
      'woodcut' -> {
        showIntInput(getLocaleString('timeDays'), availableDays).then((days) {
          showProgress(getLocaleString('woodcut')).then((value) {
            final lootBox = { inventory: {} }
            var metBeast = false
            var i = 0
            while(i < days) {
              ++i
              updateGame(ticks: kTicksPerDay)
              characterCostStamina(hero, kTicksPerDay)
              when (terrain.kind) {
                kTerrainKindMountain -> {
                  if (random.nextDouble() < kMountainBeastProbability) {
                    metBeast = true
                    break
                  }
                  if (random.nextDouble() < kMountainWoodProbability) {
                    entityAcquireItem(lootBox, Material.wood(), incidentOccured: false)
                  }
                }
                kTerrainKindForest -> {
                  if (random.nextDouble() < kForestBeastProbability) {
                    metBeast = true
                    break
                  }
                  if (random.nextDouble() < kForestWoodProbability) {
                    entityAcquireItem(lootBox, Material.wood(), incidentOccured: false)
                  }
                }
                kTerrainKindPlain -> {
                  if (random.nextDouble() < kPlainWoodProbability) {
                    entityAcquireItem(lootBox, Material.wood(), incidentOccured: false)
                  }
                }
              }
            }
            final incidentContent = getLocaleString('characterWoodcutted', [
              hero.name,
              '${terrain.left}, ${terrain.top}',
              i,
            ])
            Incident(
              subjectIds: [hero.id],
              content: incidentContent,
              isPrivate: true,
            )
            () async {
              if (lootBox.inventory.isEmpty) {
                return showDialogByLocaleKeys(['interactionGotNothing'], character: hero)
              } else {
                characterLoot(hero, lootBox, takeAll: true)
              }
            } ().then((value) {
              if (metBeast) {
                final beast = Beast()
                final message = getLocaleString('metBeast', [beast.name])
                showDialogByStrings([message]).then((value) {
                  heroBattle(beast)
                })
              }
            })
          })
        })
      }
      // 挖矿，可能会获得：矿石、灵石
      'mine' -> {
        showIntInput(getLocaleString('timeDays'), availableDays).then((days) {
          showProgress(getLocaleString('mine')).then((value) {
            final lootBox = { inventory: {} }
            var metBeast = false
            var i = 0
            while(i < days) {
              ++i
              updateGame(ticks: kTicksPerDay)
              characterCostStamina(hero, kTicksPerDay)
              when (terrain.kind) {
                kTerrainKindMountain -> {
                  if (random.nextDouble() < kMountainBeastProbability) {
                    metBeast = true
                    break
                  }
                  if (random.nextDouble() < kMountainOreProbability) {
                    entityAcquireItem(lootBox, Material.ore(), incidentOccured: false)
                  }
                  if (random.nextDouble() < kMountainJadeProbability) {
                    entityAcquireItem(lootBox, Jade(), incidentOccured: false)
                  }
                }
                kTerrainKindForest -> {
                  if (random.nextDouble() < kForestBeastProbability) {
                    metBeast = true
                    break
                  }
                  if (random.nextDouble() < kForestOreProbability) {
                    entityAcquireItem(lootBox, Material.ore(), incidentOccured: false)
                  }
                  if (random.nextDouble() < kForestJadeProbability) {
                    entityAcquireItem(lootBox, Jade(), incidentOccured: false)
                  }
                }
                kTerrainKindPlain -> {
                  if (random.nextDouble() < kPlainOreProbability) {
                    entityAcquireItem(lootBox, Material.ore(), incidentOccured: false)
                  }
                  if (random.nextDouble() < kPlainJadeProbability) {
                    entityAcquireItem(lootBox, Jade(), incidentOccured: false)
                  }
                }
              }
            }
            final incidentContent = getLocaleString('characterMined', [
              hero.name,
              '${terrain.left}, ${terrain.top}',
              i,
            ])
            Incident(
              subjectIds: [hero.id],
              content: incidentContent,
              isPrivate: true,
            )
            () async {
              if (lootBox.inventory.isEmpty) {
                return showDialogByLocaleKeys(['interactionGotNothing'], character: hero)
              } else {
                characterLoot(hero, lootBox, takeAll: true)
              }
            } ().then((value) {
              if (metBeast) {
                final beast = Beast()
                final message = getLocaleString('metBeast', [beast.name])
                showDialogByStrings([message]).then((value) {
                  heroBattle(beast)
                })
              }
            })
          })
        })
      }
      // 捕猎，增加遇到野兽的概率200%
      'hunt' -> {
        showIntInput(getLocaleString('timeDays'), availableDays).then((days) {
          showProgress(getLocaleString('hunt')).then((value) {
            var metBeast = false
            var i = 0
            while(i < days) {
              ++i
              updateGame(ticks: kTicksPerDay)
              characterCostStamina(hero, kTicksPerDay)
              when (terrain.kind) {
                kTerrainKindMountain -> {
                  if (random.nextDouble() < (kMountainBeastProbability * kHuntBeastProbability)) {
                    metBeast = true
                    break
                  }
                }
                kTerrainKindForest -> {
                  if (random.nextDouble() < (kForestBeastProbability * kHuntBeastProbability)) {
                    metBeast = true
                    break
                  }
                }
                kTerrainKindPlain -> {
                  if (random.nextDouble() < (kPlainBeastProbability * kHuntBeastProbability)) {
                    metBeast = true
                    break
                  }
                }
              }
            }
            final incidentContent = getLocaleString('characterHunted', [
              hero.name,
              '${terrain.left}, ${terrain.top}',
              i,
            ])
            Incident(
              subjectIds: [hero.id],
              content: incidentContent,
              isPrivate: true,
            )
            if (metBeast) {
              final beast = Beast()
              final message = getLocaleString('foundBeast', [beast.name])
              showDialogByStrings([message]).then((value) {
                heroBattle(beast)
              })
            }
          })
        })
      }
      // 捕鱼，可能会获得：河鱼、海鱼、虾、蟹
      'fish' -> {
        // if (game.playerMonthly.fished.contains(terrainIndex)) {
        //   showDialogByLocaleKeys(['terrainAlreadyFished'], character: hero)
        //   return
        // }
        // game.playerMonthly.fished.add(terrainIndex)
        
        showIntInput(getLocaleString('timeDays'), availableDays).then((days) {
          showProgress(getLocaleString('fish')).then((value) {
            final lootBox = { inventory: {} }
            var metStorm = false
            var i = 0
            while(i < days) {
              ++i
              updateGame(ticks: kTicksPerDay)
              characterCostStamina(hero, kTicksPerDay)
              when (terrain.kind) {
                kTerrainKindLake -> {
                  if (random.nextDouble() < kLakeFishProbability) {
                    entityAcquireItem(lootBox, Material.fish(), incidentOccured: false)
                  }
                }
                kTerrainKindSea -> {
                  if (random.nextDouble() < kSeaStormProbability) {
                    metStorm = true
                    break
                  }
                  if (random.nextDouble() < kSeaFishProbability) {
                    entityAcquireItem(lootBox, Material.fish(), incidentOccured: false)
                  }
                }
              }
            }
            final incidentContent = getLocaleString('characterFished', [
              hero.name,
              '${terrain.left}, ${terrain.top}',
              i,
            ])
            Incident(
              subjectIds: [hero.id],
              content: incidentContent,
              isPrivate: true,
            )
            () async {
              if (lootBox.inventory.isEmpty) {
                return showDialogByLocaleKeys(['interactionGotNothing'], character: hero)
              } else {
                characterLoot(hero, lootBox, takeAll: true)
              }
            } ().then((value) {
              if (metStorm) {
                showDialogByLocaleKeys(['metStorm'], character: hero)
              }
            })
          })
        })
      }
    }
  })
}

// 返回布尔值，如果为真，则玩家控制角色会停止移动
fun handleWorldMapEntityInteraction(entityId) -> bool {
  final hero = getHero()
  final entity = game.world.entities[entityId]
  assert(entity != null)

  when (entity.encounterType) {
    // 隐藏的盗贼营地副本
    'banditCamp' -> {
      showDialogByLocaleKeys(['worldMapEncounterBanditCamp'], character: hero).then((value) {
        showSelectionByLocaleKeys(['exploreMaze', 'cancel',]).then((key) {
          when (key) {
            'exploreMaze' -> {
              final maze = game.mazes[entity.id]
              assert(maze != null)
              enterMaze(maze)
              showMaze(maze)
            }
          }
        })
      })
    }
    // 隐藏的洞天福地
    'location' -> {

    }
  }

  return false
}
