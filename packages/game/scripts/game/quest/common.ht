import '../entity/common.ht'
import '../lootbox.ht'

final kQuestDifficultyNormal = 'normal'
final kQuestDifficultyHard = 'hard'
final kQuestDifficultyExpert = 'expert'
final kQuestDifficultyMaster = 'master'
final kQuestDifficultyTorment = 'torment'
final kQuestDifficultyNightmare = 'nightmare'
final kQuestDifficultyPurgatory = 'purgatory'

const kQuestCategoryConquestMaze = 'conquestMaze' // 征讨盗贼营地
const kQuestCategoryArrest = 'arrest' // 抓捕
const kQuestCategoryTraining = 'training' // 演武
const kQuestCategoryRitual = 'ritual' // 法事
const kQuestCategoryCatering = 'catering' // 招待
const kQuestCategoryDelivery = 'delivery' // 运送人员或物品
const kQuestCategoryCraft = 'craft' // 定制

const kQuestDeliveryKindEscort = 'escort'
const kQuestDeliveryKindConvoy = 'convoy'

const kQuestConquestMazeKindKillBanditBoss = 'killBanditBoss'
const kQuestConquestMazeKindWipeBandits = 'wipeBandits'
const kQuestConquestMazeKindReturnHostage = 'returnHostage'
const kQuestConquestMazeKindReturnTreasure = 'returnTreasure'

fun generateQuestReward(difficulty) {
  final reward = Lootbox()
  return reward
}

fun characterAcceptQuest(character, site, quest) {
  // 有可能是临时生成的任务，site可能并没有这个任务，但这里并不影响。
  delete site.quests[quest.id]
  character.quests[quest.id] = quest
  character.activeQuestId ??= quest.id
  final entity = quest.questEntityAccept
  if (entity.entityType == kEntityTypeCharacter || entity.entityType == kEntityTypeNpc) {
    characterGetCompanion(character, entity)
  } else if (entity.entityType == kEntityTypeItem) {
    entityAcquireItem(character, entity)
  } else {
    engine.error('获取任务时发生错误，对象类型既不是人员也是物品：${entity.entityType}')
  }

  final questName = getLocaleString(quest.kind);
  final incidentContent = getLocaleString(
    'characterAcceptQuest',
    interpolations: [character.name, site.name, questName],
  )
  Incident(
    content: incidentContent,
    subjectIds: character.isMajorCharacter ? [character.id] : null,
  )
}

// 尝试完成任务，如果成功返回 true，并扣除任务所要求提交的物品等
fun characterTrySubmitQuest(character, target, quest) -> bool {
  if (quest.questEntitiesSubmit) {
    for (final data in quest.questEntitiesSubmit) {
      if (data.entityType == kEntityTypeCharacter || data.entityType == kEntityTypeNpc) {
        if (!character.companions.containsKey(data.id)) return false
      } else if (data.entityType == kEntityTypeItem) {
        if (!character.inventory.containsKey(data.id)) {
          return false
        } else {
          final item = character.inventory[data.id]
          if (item.stackSize < data.count) {
            return false
          }
        }
      } else {
        print(data)
        engine.error('提交任务时发生错误，对象类型既不是人员也是物品：${data.entityType}')
      }
    }

    for (final data in quest.questEntitiesSubmit) {
      if (data.entityType == kEntityTypeCharacter || data.entityType == kEntityTypeNpc) {
        final entity = character.companions[data.id]
        characterDismissCompanion(character, entity)
      } else if (data.entityType == kEntityTypeItem) {
        entityGiveItem(character, target, data.id, count: data.count)
      }
    }
  }
  return true
}

fun setCharacterActiveQuest(character, quest) {
  assert(character.quests.containsKey(quest.id))
  character.activeQuestId = quest.id
}

fun getCharacterActiveQuest(character) {
  return character.quests[character.activeQuestId]
}
