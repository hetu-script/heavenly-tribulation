import 'world/tile/tilemap.ht'
import 'datetime.ht'
import 'player.ht'

export 'random.ht'

/// 游戏设置，和存档无关，属于载入存档或者开始新游戏之前的全局设置
let isDebugMode
let isNewGame

let game
// game.characters[game.heroId]
let hero
/// 这个值仅在战斗场景中会被赋值，用于保存当前战斗的敌人
let enemy

let universe
// universe[game.currentWorldId]
let world

let history
// history[game.currentWorldId]
let timeline

let year
let ticksOfYear
let month
let ticksOfMonth
let day
let ticksOfDay

function getMonthlyIdentifiedCards() {
  return game.playerMonthly.identifiedEnemyCards
}

function useIdentifyCard() {
  ++game.playerMonthly.identifiedEnemyCards
}

struct MonthlyActivity {
  constructor {
      // 战斗操作
    this.identifiedEnemyCards = 0

    // 对其他角色操作
    this.talked = []
    this.gifted = []
    this.practiced = []
    this.consulted = []
    this.requested = []
    this.insulted = []
    this.stolen = []

    // 对某个建筑操作
    this.worked = []

    // 对自己组织操作
    this.recruited = []
  }
}

struct Game {
  constructor ({saveName}) {
    this.saveName = saveName
    this.isNewGame = true
    this.currentWorldId = null
    this.heroId = null

    // 本次游戏加载的 module，这里是纯数据，函数保存在另外的地方
    this.modules = {}

    this.deceased = {}
    this.babies = {}
    this.characters = {}
    this.organizations = {}
    // 具有唯一性的物品，材料之类不在此列
    this.items = {}

    // 游戏本身逻辑所用到的一些事件检查选项
    // 例如每个月是否进行了某个动作之类
    this.playerMonthly = MonthlyActivity()
    
    // 英雄的住所，直接保存在顶层
    // 但其实这里没有任何数据
    // this.heroHomeSite = {
    //   category: kLocationKindHome,
    //   name: engine.locale(kLocationKindHome),
    //   background: 'location/site/home.png',
    //   image: 'location/site/home_card.png',
    //   id: kLocationKindHome,
    // }
  }
}

/// 创建新游戏，重置所有状态，返回game
function createGame(saveName) {
  engine.debug('准备开始新游戏')
  game = Game(saveName: saveName)
  universe = {}
  history = {}

  hero = null
  world = null
  timeline = []

  year = 1
  month = 1
  day = 1
  ticksOfYear = 1
  ticksOfMonth = 1
  ticksOfDay = 1

  return game
}

function resetPlayerMonthlyActivities() {
  game.playerMonthly = MonthlyActivity()
}

function calculateTimestamp() {
  year = toYear(game.timestamp) + 1
  month = toMonth(game.timestamp) + 1
  day = toDay(game.timestamp) + 1
  ticksOfYear = (game.timestamp % kTicksPerYear) + 1
  ticksOfMonth = (game.timestamp % kTicksPerMonth) + 1
  ticksOfDay = (game.timestamp % kTicksPerDay) + 1
}

function clearAllModules() {
  game.modules.clear()
}

function addModule(module) {
  engine.debug('添加模组元信息 [${module.id}(ver-${module.version})]...')
  assert(module.id != null)
  // assert(game.modules.containsKey(module.id))
  // game.modules[module.id] ??= {}
  // Object.assign(game.modules[module.id], module)
  game.modules[module.id] = module
}

/// 载入游戏数据，返回game
function loadGameFromJsonData({
  gameData,
  universeData,
  historyData,
  // isEditorMode,
}) -> List {
  game = Object.fromJSON(gameData)
  game.isNewGame ??= true
  
  universe = Object.create(universeData)
  history = Object.create(historyData)
  
  assert(game.currentWorldId != null)
  // if (!isEditorMode) {
  //   assert(game.heroId != null)
  // }

  world = universe[game.currentWorldId]
  timeline = history[game.currentWorldId]
  assert(world != null)
  assert(timeline is List)
  
  engine.debug('当前世界为: [${world.id}]')

  if (game.heroId != null) {
    hero = game.characters[game.heroId]
    engine.debug('当前英雄为: [${hero.name}]')
  }

  return game
}

function addWorld(newWorld) {
  world = universe[newWorld.id] = newWorld
  timeline = history[newWorld.id] = []

  game.currentWorldId = newWorld.id
}

function switchWorld(worldId) {
  assert(universe.containsKey(worldId))
  world = universe[worldId]

  assert(history.containsKey(world.id))
  timeline = history[world.id]
  
  game.currentWorldId = world.id

  return world
}

function getMapComponents() {
  return world.components
}

/// 为地图创建显示组件，注意这里仅限非角色类装饰性数据
function createMapComponent(data, left, top) {
  assert(data.entityType == null)
  final component = Object.fromJSON(data)
  engine.info('添加显示组件：[${component.id}]')
  component.worldPosition = { left, top }
  world.components.add(component)
  return component
}

function removeMapComponentByPosition(left, top) {
  engine.info('移除显示组件，位于: [${left}, ${top}]')
  world.components.removeWhere((c) => c.worldPosition.left == left && c.worldPosition.top == top)
}

function removeMapComponentById(id) {
  engine.info('移除显示组件，id: [${id}]')
  world.components.removeWhere((c) => c.id == id)
}

function getSaveName() {
  return game.saveName
}

function setSaveName(name) {
  engine.debug('存档名设置为：${name}')
  game.saveName = name
}

function getModule(id) {
  return game.modules[id]
}

function getPlayerMonthlyActivities {
  return game.flags.playerMonthly
}

function getGameJsonData() {
  return game.toJSON()
}

function getUniverseJsonData() {
  return universe.toJSON()
}

function getHistoryJsonData() {
  return history.toJSON()
}

function getTimestamp {
  return game.timestamp
}

function getHeroId() {
  return game.heroId
}

function setHeroId(id: string) {
  assert(game.characters.containsKey(id))
  if (hero != null) {
    game.playerMonthly = MonthlyActivity()
  }

  hero = game.characters[id]

  Player.updateStats()
  engine.debug('设置当前玩家人物为 [${hero.id}]。')
  // game.heroHomeSite.locationId = hero.homeId
  game.heroId = id
}

function setHeroWorldId(worldId) {
  hero.worldId = worldId
}

function getHeroHome {
  return world.locations[hero.homeId]
}

function getHeroHomeId {
  return hero?.homeId
}

// function getHeroHomeSite {
//   return game.heroHomeSite
// }

function getHeroOrganization {
  if (hero.organizationId) {
    return game.organizations[hero.organizationId]
  } else {
    return null
  }
}

function getHeroLightedArea {
  return hero.stats.lightRadius
}

// function getHeroCultivationGenre {
//   return hero.cultivationGenre
// }

function getTerrainByIndex(index, {worldId}) {
  let atWorld = worldId != null ? universe[worldId] : world
  return world.terrains[index]
}

function getTerrainByWorldPosition(left, top, {worldId}) {
  let atWorld = worldId != null ? universe[worldId] : world
  return world.terrains[tilePos2Index(left, top, world.width)]
}

function getWorldIds {
  return universe.keys
}

function setCurrentWorldId(id) {
  engine.debug('设置当前世界为: [${id}]')
  game.currentWorldId = id
  world = universe[id]
  timeline = history[id]
}

function deleteWorldById(id) {
  assert(world.id != id)
  engine.warn('删除世界数据: [${id}]')
  universe.remove(id)
  history.remove(id)
}

function getCurrentWorldId {
  return game.currentWorldId
}

function getWorldSize({worldId}) {
  let atWorld = worldId != null ? universe[worldId] : world
  return {
    width: atWorld.width,
    height: atWorld.height,
  }
}

function getZones({worldId}) {
  let atWorld = worldId != null ? universe[worldId] : world
  return atWorld.zones.values
}

function getZoneById(id: string, {worldId}) {
  let atWorld = worldId != null ? universe[worldId] : world
  return atWorld.zones[id]
}

function addObject(object, {worldId}) {
  let atWorld = worldId != null ? universe[worldId] : world
  atWorld.objects[object.id] = object
}

function removeObjectById(id, {worldId}) {
  let atWorld = worldId != null ? universe[worldId] : world
  delete atWorld.objects[id]
}

function getObjects({worldId}) {
  let atWorld = worldId != null ? universe[worldId] : world
  return atWorld.objects.values
}

function getObjectById(id: string, {worldId}) {
  let atWorld = worldId != null ? universe[worldId] : world
  return atWorld.objects[id]
}

function hasObject(id: string, {worldId}) {
  let atWorld = worldId != null ? universe[worldId] : world
  return atWorld.objects[id] != null
}

function getNameFromId(id, [orElse = 'null']) {
  if (id != null) {
    return id.split('.').last
  } else {
    return engine.locale(orElse)
  }
}

function onNewGame() {
  game.isNewGame = false
  onWorldEvent('onNewGame')
}
