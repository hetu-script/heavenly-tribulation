import 'game.ht'
import 'colors.ht'
import 'binding/world.ht'
import 'entity/entity.ht'

external function expForLevel(level: integer) -> integer

const kQuickThreshold = 10
const kSlowThreshold = 10
const kNimbleThreshold = 10
const kClumsyThreshold = 10

struct PassiveData {
  constructor ({
    id,
    priority,
    level,
    value,
    increment,
    description,
  }) {
    assert(id != null && description != null)
    this.id = id
    this.priority = priority ?? 0
    this.level = level ?? 0
    this.value = value
    this.increment = increment
    this.description = description
  }
}

namespace Player {
  
  external function updateHero()

  external function updateHistory()

  external function updateQuest()

  function setTo(left, top, { worldId, direction } ) {
    setCharacterWorldPosition(hero, left, top, worldId: worldId)
    if (worldId == null || worldId == world.id) {
      if (direction != null) {
        assert(kOrthogonalDirections.contains(direction))
      }
      World.setCharacterTo(hero.id, left, top, direction: direction)
      World.lightUpAroundTile(left, top, hero.stats.lightRadius)
    }
  }

  function collectById(materialId, { amount = 1, incurIncident = true }) {
    assert(amount is integer && amount > 0)
    hero.materials[materialId] += amount
    
    let incidentContent = engine.locale('heroAcquireMultiple', interpolations: [
      amount,
      engine.locale(materialId),
    ])
    if (incurIncident) {
      Incident(
        message: incidentContent,
        subjectId: hero.id,
        isPrivate: true,
      )
    }
    engine.debug(incidentContent)
  }

  /// 从物品库中复制并获取一个物品
  function acquireItemById(itemId, { incurIncident = true }) {
    final item = createItemById(itemId)

    hero.inventory[item.id] = item
    item.ownerId = hero.id
    
    let incidentContent
    incidentContent = engine.locale('heroAcquire', interpolations: [
      item.name,
    ])
    if (incurIncident) {
      Incident(
        message: incidentContent,
        subjectId: hero.id,
        item: item,
        isPrivate: true,
      )
    }
    engine.debug(incidentContent)
  }

  /// 直接获取一个已经被创造出来的物品
  function acquire(item, { incurIncident = true }) {
    // 刚获得的卡牌会显示发光特效，第一次鼠标移上去之后会移除
    item.isNew = true
    switch(item.entityType) {
      case kEntityTypeItem: {
        item.ownerId = hero.id
        hero.inventory[item.id] = item
        item.acquiredSequence = hero.achievements.acquiredItems
        ++hero.achievements.acquiredBattleCards
      }
      case kEntityTypeBattleCard: {
        hero.cardLibrary[item.id] = item
        item.acquiredSequence = hero.achievements.acquiredBattleCards
        ++hero.achievements.acquiredBattleCards
      }
    }
    
    let incidentContent
    incidentContent = engine.locale('heroAcquire', interpolations: [
      item.name,
    ])
    if (incurIncident) {
      Incident(
        message: incidentContent,
        subjectId: hero.id,
        item: item,
        isPrivate: true,
      )
    }
    engine.debug(incidentContent)
  }

  function lose(item, { incurIncident = true }) {
    assert(item.ownerId == null || item.ownerId == hero.id)
    delete hero.inventory[item.id]
    delete item.ownerId

    let incidentContent
    incidentContent = engine.locale('heroLose', interpolations: [
      item.name,
    ])
    if (incurIncident) {
      Incident(
        message: incidentContent,
        subjectId: hero.id,
        item: item,
        isPrivate: true,
      )
    }
    engine.debug(incidentContent)
  }

  // 使用食物或者丹药
  // 返回值代表是否使用成功
  // 此函数允许对一个不在物品栏中的物品调用
  // function consume(item, { forcerCharacter }) {
  //   if (!item.isConsumable) {
  //     engine.warn('${item.id} 无法食用。')
  //     return false
  //   }

  //   if (hero.inventory.containsKey(item.id)) {
  //     if (item.rarity == kCommon) {
  //       if (item.stackSize > 1) {
  //         --item.stackSize
  //       } else {
  //         delete hero.inventory[item.id]
  //       }
  //     } else {
  //       delete hero.inventory[item.id]
  //     }
  //   }

  //   // if (item.stats.effects) {
  //   //   for (const effect of item.stats.effects) {
  //   //     // 即便是可以食用的物品，也可能包含一些非食用效果，因此这里要加以判断
  //   //     if (effect.category == kEffectCategoryConsume) {
  //   //       const effectHandler = getEffectHandlerById(effect.id)
  //   //       if (effectHandler is function) effectHandler(effect, character)
  //   //     }
  //   //   }
  //   // }

  //   let incidentContent
  //   switch (item.kind) {
  //     kEntityConsumableKindMedicine : {
  //       incidentContent = forcerCharacter ?
  //         engine.locale('characterTakeMedicineForced', interpolations: [
  //           forcerCharacter.name,
  //           character.name,
  //           item.name,
  //         ]) :
  //         engine.locale('characterTakeMedicine', interpolations: [
  //           character.name,
  //           item.name,
  //         ])
  //     }
  //     kEntityConsumableKindBeverage : {
  //       incidentContent = forcerCharacter ?
  //         engine.locale('characterDrinkBeverageForced', interpolations: [
  //           forcerCharacter.name,
  //           character.name,
  //           item.name,
  //         ]) :
  //         engine.locale('characterDrinkBeverage', interpolations: [
  //           character.name,
  //           item.name,
  //         ])
  //     }
  //     else : {
  //       incidentContent = forcerCharacter ?
  //         engine.locale('characterEatForced', interpolations: [
  //           forcerCharacter.name,
  //           character.name,
  //           item.name,
  //         ]) :
  //         engine.locale('characterEat', interpolations: [
  //           character.name,
  //           item.name,
  //         ])
  //     }
  //   }

  //   let subjectIds
  //   let objectIds
  //   if (forcerCharacter && forcerCharacter.isMajorCharacter) {
  //     subjectIds = [forcerCharacter.id]
  //     if (character.isMajorCharacter) {
  //       objectIds = [character.id]
  //     }
  //   } else if (character.isMajorCharacter) {
  //     subjectIds = [character.id]
  //   }

  //   Incident(
  //     message: incidentContent,
  //     subjectIds: subjectIds,
  //     objectIds: objectIds,
  //     isPrivate: true,
  //   )
    
  //   return true
  // }

  // 法宝和技能都可以装备，只要类型符合
  // 返回值代表是否装备成功
  // 如果对一个不在物品栏中的物品调用，会先将其加入物品栏
  // 但并不会检查是否已经在别人的物品栏里了
  // 
  // 装备并非直接将物品数据保存在人物数据中，而是用下面的数据结构
  // {
  //   id,
  //   entityType,
  //   category,
  //   kind,
  //   equipType,
  //   life,
  // }
  function equip(item, { index }) -> bool {
    assert(index == null || (index >= 0 && index < kEquipmentMax))
    if (item.category != kItemCategoryEquipment) {
      engine.warn('${item.id} 无法装备。')
      return false
    }
    if (item.equippedPosition) {
      engine.warn('${item.id} 已经在装备中了。')
      return false
    }

    let equipped = false
    // 如果有空位就装备在空位上
    if (index == null) {
      for (const i in range(kEquipmentMax)) {
        if (hero.equipments[i] == null) {
          hero.equipments[i] = item.id
          item.equippedPosition = i
          equipped = true
          break
        }
      }
      // 否则替换掉第一个空位
      index = 0
    }

    if (!equipped) {
      assert(index != null)

      const oldEquipId = hero.equipments[index]
      if (oldEquipId != null) {
        const oldItem = hero.inventory[oldEquipId]
        delete oldItem.equippedPosition
      }
      hero.equipments[index] = item.id
      item.equippedPosition = index
      equipped = true
    }

    if (equipped) {
      const incidentContent = engine.locale('heroEquip', interpolations: [
        item.name,
      ])
      Incident(
        message: incidentContent,
        subjectId: hero.id,
        isPrivate: true,
      )

      for (final passiveData in item.affixes) {
        gainPassive(passiveData.id, level: passiveData.level)
      }

      // 重新计算角色状态值
      updateStats(hero)
    }

    return equipped
  }

  function unequip(item) {
    assert(item.equippedPosition != null)
    hero.equipments[item.equippedPosition] = null
    delete item.equippedPosition

    for (final passiveData in item.affixes) {
      refundPassive(passiveData.id, level: passiveData.level)
    }

    updateStats(hero)

    const incidentContent = engine.locale('heroUnequip', interpolations: [
      item.name,
    ])
    Incident(
      message: incidentContent,
      subjectId: hero.id,
      isPrivate: true,
    )
  }

  // 批量获取物品，但不显示从那里获得的，通常用于从箱子或包裹中批量拿取
  function loot(lootbox, { incurIncident = true }) {
    for (const item of lootbox.inventory) {
      acquire(item, incurIncident: incurIncident)
    }
    // if (lootbox.money) {
    //   acquireMoney(character, lootbox.money, incurIncident: incurIncident)
    // }

    // 清空箱子
    lootbox.inventory.clear()

    // const ids = lootbox.keys.toList()
    // for (const id in ids) {
    //   delete lootbox[id]
    // }
  }

  // 设定人物的随从，返回值是否成功
  // 随从并非直接将人物数据放在人物上，而是用下面的数据结构
  // {
  //   id,
  //   entityType,
  //   coordination,
  // }
  function acquireCompanion(companion, { incurIncident = true }) {
    if (hero.companions.contains(companion.id)) {
      engine.warn('${companion.id} 已经是随从了，无法再次添加。')
      return
    }
    hero.companions.add(companion.id)
    
    const incidentContent = engine.locale('heroAcquireCompanion', interpolations: [
      companion.name,
    ])
    if (incurIncident) {
      Incident(
        message: incidentContent,
        subjectId: companion.id,
        objectId: hero.id,
        isPrivate: true,
      )
    }
    engine.debug(incidentContent) 
  }

  function dismissCompanion(companion, { incurIncident = true }) {
    assert(hero.companions.contains(companion.id))

    hero.companions.remove(companion.id)
    
    const incidentContent = engine.locale('heroLoseCompanion', interpolations: [
      companion.name,
    ])
    if (incurIncident) {
      Incident(
        message: incidentContent,
        subjectId: companion.id,
        objectId: hero.id,
        isPrivate: true,
      )
    }
    engine.debug(incidentContent)
  }

  function cultivationLevelUp({ incurIncident = true }) {
    let expRequirement = expForLevel(hero.cultivationLevel)
    if (hero.exp < expRequirement) {
      engine.warn('not enough exp for level up! current level: [${hero.cultivationLevel}], exp needed: [${expRequirement}]')
      return
    }

    if (hero.cultivationLevel == hero.cultivationLevelMax) {
      engine.warn('character reached max level for current rank! current level: [${hero.cultivationLevel}], current rank: [${hero.cultivationRank}]')
      return
    }

    hero.exp -= expRequirement
    ++hero.cultivationLevel

    hero.availableSkillPoints += 1

    const incidentContent = engine.locale('heroCultivationLevelUp', interpolations: [
      hero.cultivationLevel,
    ])
    if (incurIncident) {
      Incident(
        message: incidentContent,
        subjectId: hero.id,
        isPrivate: true,
      )
    }
    engine.debug(incidentContent)

    return hero.cultivationLevel
  }

  function cultivationRankUp({ incurIncident = true }) {
    if ( hero.cultivationRank >= kRankMax ) {
      engine.warn('character reached max rank! current rank: [${hero.cultivationRank}]')
      return
    }

    ++hero.cultivationRank
    
    hero.cultivationLevelMax = maxLevelPerRank(hero.cultivationRank)

    const incidentContent = engine.locale('heroCultivationRankUp', interpolations: [
      getCultivationRankName(character),
    ])
    if (incurIncident) {
      Incident(
        message: incidentContent,
        subjectId: hero.id,
        isPrivate: true,
      )
    }
    engine.debug(incidentContent)

    return hero.cultivationRank
  }

  function gainExp(value: integer) {
    assert(value > 0)
    hero.unconvertedExp += value

    const incidentContent = engine.locale('heroExpGained', interpolations: [value])
    Incident(
      message: incidentContent,
      subjectId: hero.id,
      isPrivate: true,
    )

    World.addHintText(
      '${engine.locale('exp')} + ${value}',
      hero.worldPosition.left,
      hero.worldPosition.top,
      Colors.blue,
    )
  }

  /// 提升玩家的天赋技能
  /// 注意此函数调用时并不会检测天赋树节点位置和链接情况，也不会检查角色是否有足够的技能点
  /// 玩家升级天赋时，并不会立刻影响角色数据，
  /// 只有在执行 updateStats() 之后，人物数据才会最终计算出来
  function gainPassive(dataId, {level = 1}) {
    assert(level > 0)
    if (hero.passives[dataId] == null) {
      final passiveRawData = game.passives[dataId]
      assert(passiveRawData != null)
      final passiveData = PassiveData(
        id: dataId,
        priority: passiveRawData.priority ?? 0,
        level: level,
        description: passiveRawData.description,
      )
      if (passiveRawData.increment != null) {
        passiveData.increment = passiveRawData.increment
        passiveData.value = (passiveData.increment * level).round()
      }
      hero.passives[dataId] = passiveData
    } else {
      final passiveData = hero.passives[dataId]
      passiveData.level += level
      passiveData.value = (passiveData.increment * passiveData.level).round()
    }
  }

  function refundPassive(dataId, {level = 1}) {
    if (hero.passives[dataId] == null) {
      engine.warn('dataId [${dataId}] not found on hero passives data for refund!')
      return
    }
    final passiveData = hero.passives[dataId]
    passiveData.level -= level
    if (passiveData.level <= 0) {
      hero.passives[dataId] = null
    } else if (passiveData.value != null) {
        passiveData.value = (passiveData.increment * passiveData.level).round()
    }
  }

  function updateStats() {
    updateCharacterStats(hero)
  }

  function rejuvenate() {
    rejuvenateCharacter(hero)
  }
}
