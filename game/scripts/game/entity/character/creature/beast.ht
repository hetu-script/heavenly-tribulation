import '../battle_entity.ht'
import '../../../name/creature.ht'
import '../../item/talisman/weapon/beast_weapon.ht'
import '../../common.ht'

const _kBeastRandomAgeMax = 150

const _kBeastAttributeSum = 200

const _kBeastTypes = {
  tiger: {
    spirituality: 0,
    dexterity: 88,
    strength: 92,
    willpower: 0,
    perception: 77,
  },
  monkey: {
    spirituality: 0,
    dexterity: 84,
    strength: 55,
    willpower: 0,
    perception: 85,
  },
  snake: {
    spirituality: 0,
    dexterity: 42,
    strength: 74,
    willpower: 0,
    perception: 96,
  },
}

struct Beast {
  construct ({
    birthTimestamp,
    kind,
    attributes,
    rarity,
    description,
  }) {
    this.entityType = kEntityTypeNpc
    this.birthTimestamp = birthTimestamp ?? createRandomBirthTimestamp(max: _kBeastRandomAgeMax)
    this.kind = kind ?? random.nextIterable(_kBeastTypes.keys)
    const randomName = generateCreatureName(category: this.kind, rarity: rarity)
    this.name = randomName.name
    this.category = kEntityCategoryBeast
    this.id = '${this.entityType}.${crypto.randomUID4(2)}.${this.name}'
    this.rarity = randomName.rarity ?? kCommon
    this.color = kRarity[this.rarity].color
    game.minorCharacters[this.id] = this

    this.description = description ?? ''

    // 生成战斗单位相关数据，并写入当前对象
    // 不使用继承主要是因为这些数据经常会改动，直接写在这个对象上的话会更清晰
    const battleInfo = BattleEntity(
      attributes: attributes ?? _kBeastTypes[this.kind],
      attributeSum: _kBeastAttributeSum,
      generate: false,
    )
    Object.assign(this, battleInfo)

    // 装备栏由战斗单位类生成，因此下面的代码不能放在 BattleEntity 之前
    switch (this.kind) {
      'tiger' : {
        this.icon = 'maze/tiger.png'
        equip(this, BeastWeapon(beastName: this.name, kind: kBeastWeaponKindClaw, rarity: this.rarity))
        equip(this, BeastWeapon(beastName: this.name, kind: kBeastWeaponKindFang, rarity: this.rarity))
      }
      'monkey' : {
        this.icon = 'maze/monkey.png'
        equip(this, BeastWeapon(beastName: this.name, kind: kBeastWeaponKindClaw, rarity: this.rarity))
      }
      'snake' : {
        this.icon = 'maze/snake.png'
        equip(this, BeastWeapon(beastName: this.name, kind: kBeastWeaponKindFang, rarity: this.rarity))
      }
    }

    calculateCharacterStats(this)
  }
}
