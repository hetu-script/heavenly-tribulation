import '../battle_entity.ht'
import '../../common.ht'
import '../../item/item.ht'
import '../companion.ht'
import '../../../game.ht'

final _kBanditKinds = Set(
  'boss', // level 3 出口的 boss
  'killer', // level 2 出口的 boss
  'muscle', // level 1 出口的 boss
  'minion',
)

const _kBanditName = 'bandit'
const _kPirateName = 'pirate'

const _kBanditAttributeSum = 300

final _kBeastAttributes = {
  minion: {
    strength: 79,
    constitution: 75,
    spirituality: 0,
    willpower: 44,
    dexterity: 51,
    perception: 63,
  },
  muscle: {
    strength: 92,
    constitution: 89,
    spirituality: 0,
    willpower: 73,
    dexterity: 60,
    perception: 47,
  },
  killer: {
    strength: 80,
    constitution: 89,
    spirituality: 0,
    willpower: 77,
    dexterity: 84,
    perception: 85,
  },
  boss: {
    strength: 95,
    constitution: 92,
    spirituality: 0,
    willpower: 80,
    dexterity: 72,
    perception: 86,
  },
}

struct Bandit {
  construct ({
    birthTimestamp,
    isPirate,
    kind,
    rarity,
    description,
    attributes,
    attributeSum,
  }) {
    assert(kind in _kBanditKinds)
    this.kind = kind
    this.entityType = kEntityTypeNpc
    this.birthTimestamp = birthTimestamp ?? createRandomBirthTimestamp()
    // this.icon = 'avatar/general.png'
    this.category = kEntityCategoryCharacter
    this.name = getLocaleString(isPirate ? _kPirateName : _kBanditName) + getLocaleString(this.kind)
    this.id = '${this.entityType}.${Hash.uid4(2)}.${this.name}'
    this.rarity = rarity ?? kCommon
    this.color = kRarity[this.rarity].color
    this.description = description ?? ''
    game.npcs[this.id] = this

    // 装备栏由战斗单位类生成，因此下面的代码不能放在 BattleEntity 之前
    when (this.kind) {
      'minion' -> {
        this.icon = 'maze/bandit_minion.png'
        final battleInfo = BattleEntity(
          attributes: attributes ?? _kBeastAttributes.minion,
          attributeSum: _kBanditAttributeSum,
          generate: false,
          baseLife: 125,
        )
        this.assign(battleInfo)
        // BattleEntity 会初始化银两为0，因此必须在其之后赋值
        acquireMoney(this, random.nextInt(50) + 50)
        equip(this, Weapon())
        // 山贼喽啰可以成为别的山贼的战斗伙伴
        this.isEquippable = true
        this.equipType = kEquipTypeCompanion
        this.companionType = kCompanionTypeBattle
      }
      'muscle' -> {
        this.icon = 'maze/bandit_muscle.png'
        final battleInfo = BattleEntity(
          attributes: attributes ?? _kBeastAttributes.muscle,
          attributeSum: _kBanditAttributeSum,
          generate: false,
          baseLife: 175,
        )
        this.assign(battleInfo)
        // BattleEntity 会初始化银两为0，因此必须在其之后赋值
        acquireMoney(this, random.nextInt(75) + 50)
        equip(this, Weapon(kind: 'spear'))
        final minion1 = Bandit(kind: 'minion')
        equip(this, minion1)
      }
      'killer' -> {
        this.icon = 'maze/bandit_killer.png'
        final battleInfo = BattleEntity(
          attributes: attributes ?? _kBeastAttributes.killer,
          attributeSum: _kBanditAttributeSum,
          generate: false,
          baseLife: 225,
        )
        this.assign(battleInfo)
        // BattleEntity 会初始化银两为0，因此必须在其之后赋值
        acquireMoney(this, random.nextInt(100) + 50)
        equip(this, Weapon(kind: 'sabre'))
        equip(this, Weapon(kind: 'sabre'))
        final minion1 = Bandit(kind: 'minion')
        equip(this, minion1)
      }
      'boss' -> {
        final battleInfo = BattleEntity(
          attributes: attributes ?? _kBeastAttributes.boss,
          attributeSum: _kBanditAttributeSum,
          generate: false,
          baseLife: 300,
        )
        this.assign(battleInfo)
        // BattleEntity 会初始化银两为0，因此必须在其之后赋值
        acquireMoney(this, random.nextInt(300) + 100)
        this.icon = 'maze/bandit_boss.png'
        equip(this, Weapon())
        equip(this, Shield())
        final minion1 = Bandit(kind: 'minion')
        final minion2 = Bandit(kind: 'minion')
        final minion3 = Bandit(kind: 'minion')
        equip(this, minion1)
        equip(this, minion2)
        equip(this, minion3)
      }
    }
    
    calculateCharacterStats(this)
  }
}
