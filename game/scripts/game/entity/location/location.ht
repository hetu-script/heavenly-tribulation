import '../../game.ht'
import '../character/common.ht'
import '../../dialog.ht'
import '../../name/location.ht'
import '../../l10n.ht'
import '../common.ht'
import '../../tile/hexagonal_tile.ht'
import 'site.ht'
import '../../tile/common.ht'

const _kLocationInitialStabilityMax = 50

const kLocationCategoryCity = 'city'
const kLocationCategoryIsland = 'island'
const kLocationCategoryMountain = 'mountain'

const kLocationDevelopmentMax = 9

struct Location {
  construct ({
    terrainIndex,
    left,
    top,
    category,
    name,
  }) {
    assert(terrainIndex != null)
    assert(left != null && top != null)

    // 该据点的每月刷新时间
    this.monthlyUpdateTime = random.nextInt(kTicksPerMonth)
    // 该据点的事件触发选项
    this.flags = {
      recruitingOrganizationIds: [],
    }

    this.entityType = kEntityTypeLocation
    this.terrainIndex = terrainIndex
    this.tilePosition = Tile(
      left,
      top,
      index: terrainIndex,
      mapWidth: currentWorld.width,
    )
    this.index = game.locations.length
    if (name) {
      this.name = name
    } else {
      let conflict = false
      do {
        const randomName = generateLocationName(category: category)
        this.name = randomName.name
        conflict = game.locations.values.where((element) =>
          element.name == this.name).isNotEmpty
      } while (conflict)
    }
    this.category = category
    this.id = '${this.entityType}.${this.index}.${this.name}'
    game.locations[this.id] = this

    // 据点稳定度，最高100，初始随机0-49
    // 稳定度决定了盗贼和动乱事件的可能性。
    this.stability = random.nextInt(_kLocationInitialStabilityMax)
    
    // 据点发展度，最高20，初始0
    // 发展度决定了据点所能拥有的普通居民人口上限，所能拥有建筑的数量，以及能产生的悬赏任务上限等
    // 发展度需要花费银两、矿石和木料来升级
    this.development = 0
    this.pupulation = maxPopulationOfLocationDevelopment

    // 据点和人物类似，也具有金钱和物品栏
    // this.money = 0
    this.inventory = {}

    // 当前处于这个场景的角色
    this.dwellingCharacterIds = []

    // 居住在这个场景的角色
    // this.residingCharacterIds = Set()

    // 在野角色
    this.residingCharacterIds = []

    // 大牢中的角色
    // 如果主角自己进入了监牢，则要通过监牢副本才能离开，并且离开后会进入本据点的敌对名单。
    // 另外两种选择是通过诉讼，或者贿赂离开
    this.jailedCharacterIds = []
    
    // 这个据点的建筑
    this.sites = {}

    // 这个地点的组织
    this.organizations = {}

    // 如果是盗贼营地或者洞天福地，则默认不会显示在地图上，并且也不会默认添加民居
    // 隐藏的洞天会显示为山峰，但隐藏的福地只有被发现后才会出现岛屿，之前就是普通的水面
    if (this.category == kLocationCategoryCity) {
      Site(category: kSiteResidence, locationId: this.id)
      // 普通居民决定了税收收入，以及外门弟子的数量
      // const minP = maxPopulationOfLocationDevelopment(this.development - 1)
      // const maxP = maxPopulationOfLocationDevelopment(this.development)
      // this.population = random.nextInt(maxP - minP) + minP
    }

    // 被禁止进入的名单，名单可能重叠
    // 一个人本身在名单上，但他的国家也可能在名单上，并且互相独立
    this.banLists = {
      characterIds: [],
      organizationIds: [],
      // nationIds: [],
    }
  }
}

function getLocationDistance(loc1, loc2) {
  return getTileDistance(loc1.tilePosition, loc2.tilePosition)
}

// 每个城市发展度对应的居民上限
// 居民可以用于充当据点的仆役
function maxPopulationOfLocationDevelopment(n: int) {
  return (n - 1) * (n - 1) * (n - 1) * 50 + 100
}

function maxSiteNumberOfLocationDevelopment(n: int) {
  return (n + 1) * n ~/ 2 + 1
}

// 每个据点，每个月会自然增加人口，直到达到上限
// 人口上限会受到掌门的技能，门派的发展度和价值观，以及据点的发展度和建筑所影响
function increaseLocationPopulationPerMonth(location) {

}

function addLocation(loc) {
  game.locations[loc.id] = loc
}

function addLocations(locs: List) {
  engine.info('载入地点数据')
  for (const loc in locs) {
    if (loc.nameId) {
      loc.name = getLocaleString(loc.nameId)
    }
    game.locations[loc.id] = loc
  }
}

function setCharacterLocationId(character, locationId: str) {
  assert(game.locations.containsKey(locationId))
  if (character.locationId != null) {
    const oldLocation = game.locations[character.locationId]
    oldLocation.dwellingCharacterIds.remove(character.id)
  }
  const location = game.locations[locationId]
  location.dwellingCharacterIds.add(character.id)
  character.locationId = locationId
  character.tilePosition = {}
}

function getLocationById(id: str) {
  return game.locations[id]
}

function getLocations() {
  return game.locations
}

function getLocationByPosition(left, top) {
  const tile = currentWorld.terrains[tilePos2Index(left, top, currentWorld.width)]
  if (tile.locationId)
  return game.locations[tile.locationId]
}

function getLocationSiteById(location, siteId: str) {
  assert(location.sites.containsKey(siteId) || siteId)
  return location.sites[siteId]
}

function discoverLocation(location) {
  if (location.flags.isDiscovered) return
  location.flags.isDiscovered = true
  WorldMap.setWorldMapCaption(location.tilePosition.left, location.tilePosition.top, location.name)
  if (location.category == kLocationCategoryIsland) {
    const tile = currentWorld.terrains[location.terrainIndex]
    // tile.spriteIndex = kSpriteLand
    tile.overlaySprite.spriteIndex = kSpriteCity
    // WorldMap.setWorldMapSprite(location.tilePosition.left, location.tilePosition.top, kSpriteLand)
    WorldMap.setWorldMapOverlaySprite(
      location.tilePosition.left,
      location.tilePosition.top, 
      {
        spriteIndex: kSpriteCity,
      },
    )
  }
}

function increaseLocationDevelopment(location) {
  if (location.development < kLocationDevelopmentMax) {
    ++location.development
  } else {
    engine.warning('据点 ${location.name} 的发展度（${location.development}）已达到或超过最大值。')
  }
}
