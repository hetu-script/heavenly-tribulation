// import 'tilemap.ht'

struct Tile {
  constructor (left, top, {index, mapWidth}) {
    assert(left != null && top != null)
    if (index) {
      this.index = index
    } else if (mapWidth) {
      this.index = tilePos2Index(left, top, mapWidth)
    }
    this.left = left
    this.top = top
    // 切换为 horizontal hexgonal tile map 的坐标系
    // 用于距离计算的函数
    // 以 (1, 1) 为原点，该格子相对向右下行的斜线的距离
    this.slashLeft = ((left.isOdd ? (left + 1) / 2 : left / 2) - top).truncate()
    // 以 (1, 1) 为原点，该格子相对向右上行的斜线的距离
    this.slashTop = left - this.slashLeft - 1
  }
}

// 计算 hexagonal tile 的方向
function getDirection(start: {}, end: {}) {
  assert(start.left != end.left || start.top != end.top)
  if (start.left % 2 != 0) {
    if (end.left == start.left) {
      if (end.top < start.top) {
        return 'north'
      } else {
        return 'south'
      }
    } else if (end.left > start.left) {
      if (end.top == start.top) {
        if (end.left % 2 != 0) {
          return 'east'
        } else {
          return 'southEast'
        }
      } else if (end.top < start.top) {
        return 'northEast'
      } else {
        return 'southEast'
      }
    } else {
      if (end.top == start.top) {
        if (end.left % 2 != 0) {
          return 'west'
        } else {
          return 'southWest'
        }
      } else if (end.top < start.top) {
        return 'northWest'
      } else {
        return 'southWest'
      }
    }
  } else {
    if (end.left == start.left) {
      if (end.top < start.top) {
        return 'north'
      } else {
        return 'south'
      }
    } else if (end.left > start.left) {
      if (end.top == start.top) {
        if (end.left.isEven) {
          return 'east'
        } else {
          return 'northEast'
        }
      } else if (end.top < start.top) {
        return 'northEast'
      } else {
        return 'southEast'
      }
    } else {
      if (end.top == start.top) {
        if (end.left.isEven) {
          return 'west'
        } else {
          return 'northWest'
        }
      } else if (end.top < start.top) {
        return 'northWest'
      } else {
        return 'southWest'
      }
    }
  }
}

// manhattan 距离算法
function getTileDistance(start, end) {
  assert(start.slashLeft != null && start.slashTop != null && end.slashLeft != null && end.slashTop != null)
  let result
  const dx = end.slashLeft - start.slashLeft
  const dy = end.slashTop - start.slashTop
  if ((dx >= 0 && dy >= 0) || (dx <= 0 && dy <= 0)) {
    result = (dx + dy).abs()
  } else {
    result = Math.max(dx.abs(), dy.abs())
  }
  // print('getTileDistance: ${start}, ${end}, result: ${result}')
  return result
}

/// 获取相邻并且在地图范围内的格子
/// 返回一个Map，index 是 1-6 (六边形地块)
/// 每个编号对应一个固定位置的相邻地块，如果值为 null 表示这个位置没有符合要求的相邻地块
/// 对于横向六边形tilemap，邻居顺序是左上(1)上(2)右上(3)右下(4)下(5)左下(6)
/// 如果 [terrainKinds] 不为 空，则只会返回指定 kind 属性范围的格子
function getMapTileNeighbors(left: integer, top: integer, { map, terrainKinds = [] }) {
  map ??= world
  const neighbors = {}

  function addNeighbor(neighborIndex, tileIndex) {
    if (tileIndex < 0 || tileIndex >= map.terrains.length) return
    final tile = map.terrains[tileIndex]
    if (terrainKinds.isEmpty || tile.kind in terrainKinds) {
      neighbors[neighborIndex] = tile
    }
  }

  if (left.isOdd) {
    // 奇数列
    // 左上
    addNeighbor(1, tilePos2Index(left - 1, top - 1, map.width))
    // 上面
    addNeighbor(2, tilePos2Index(left, top - 1, map.width))
    // 右上
    addNeighbor(3, tilePos2Index(left + 1, top - 1, map.width))
    // 右下
    addNeighbor(4, tilePos2Index(left + 1, top, map.width))
    // 下面
    addNeighbor(5, tilePos2Index(left, top + 1, map.width))
    // 左下
    addNeighbor(6, tilePos2Index(left - 1, top, map.width))
  } else {
    // 偶数列
    // 左上
    addNeighbor(1, tilePos2Index(left - 1, top, map.width))
    // 上面
    addNeighbor(2, tilePos2Index(left, top - 1, map.width))
    // 右上
    addNeighbor(3, tilePos2Index(left + 1, top, map.width))
    // 右下
    addNeighbor(4, tilePos2Index(left + 1, top + 1, map.width))
    // 下面
    addNeighbor(5, tilePos2Index(left, top + 1, map.width))
    // 左下
    addNeighbor(6, tilePos2Index(left - 1, top + 1, map.width))
  }
  return neighbors
}
