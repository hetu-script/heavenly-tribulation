import 'tile/common.ht'
import 'zone.ht'

const _kMinZoneSizeOfSea = 8
const _kMinZoneSizeOfIsland = 8

const kMapStyles = Set(['islands', 'coast', 'inland'])

/// 利用 noise 函数生成大陆和海洋的算法
function generateContinent2(world, {seed, style}) {
  let noiseData
  let threshold
  assert(style in kMapStyles)
  switch (style) {
    'islands' : {
      threshold = 0.55
      noiseData = Math.noise2d(
        world.width,
        world.height,
        seed: seed,
        noiseType: 'perlinFractal',
        frequency: 6 / ((world.width + world.height) / 2),
      )
    }
    'coast' : {
      threshold = 0.48
      noiseData = Math.noise2d(
        world.width,
        world.height,
        seed: seed,
        noiseType: 'valueFractal',
        frequency: 3.5 / ((world.width + world.height) / 2),
        octaves: 10,
      )
    }
    'inland' : {
      threshold = 0.35
      noiseData = Math.noise2d(
        world.width,
        world.height,
        seed: seed,
        noiseType: 'cubicFractal',
        frequency: 10 / ((world.width + world.height) / 2),
      )
    }
  }
  for (const y in range(world.height)) {
    for (const x in range(world.width)) {
      if (y == 0 || y == world.height - 1 || x == 0 || x == world.width - 1) continue
      const index = tilePos2Index(x + 1, y + 1, world.width)
      const noise = noiseData[x][y]
      const normalize = (noise + 1) / 2;
      if (normalize > threshold) {
        const tile = world.terrains[index]
        tile.spriteIndex = kSpriteLand
      }
    }
  }
}

function nameZones(world) {
  engine.debug('为地域命名')
  if (world.zones.isEmpty) return

  world.seaIndexes = []
  for (const zone of world.zones) {
    // kZoneSea & kZoneLand 只是临时类型，现在替换为具体的类型
    if (zone.category == kZoneSea) {
      // 将临时类型 water 根据情况替换为 sea 和 lake
      zone.category = zone.terrainIndexes.length < _kMinZoneSizeOfSea ? kZoneCategoryLake : kZoneCategorySea
      if (zone.category == kZoneCategorySea) {
        // 将所有海域保存在一个总表中
        world.seaIndexes.addAll(zone.terrainIndexes)
      }
      // 为该地域中所有地块赋值 kind
      for (const terrainIndex in zone.terrainIndexes) {
        const terrain = world.terrains[terrainIndex]
        terrain.isWater = true
        // TODO: lake类型单独的贴图
        if (terrain.kind == kZoneCategoryLake) {
          terrain.sprite = 'object/river.png'
        }
      }
    } else if (zone.category == kZoneRiver) {
      zone.category = kZoneCategoryRiver
      for (const terrainIndex in zone.terrainIndexes) {
        const terrain = world.terrains[terrainIndex]
        terrain.isWater = true
        // TODO: river类型单独的贴图
        terrain.sprite = 'object/river.png'
      }
    } else if (zone.category == kZoneLand) {
      // 将临时类型 land 根据情况替换为 continent 和 island
      zone.category = zone.terrainIndexes.length < _kMinZoneSizeOfIsland ? kZoneCategoryIsland : kZoneCategoryContinent
      // 为该地域中所有地块赋值 kind
      for (const terrainIndex in zone.terrainIndexes) {
        const terrain = world.terrains[terrainIndex]
        terrain.isWater = false
        // TODO: 道路类型单独的贴图
      }
    }

    zone.name = generateZoneName(category: zone.category)
  }
}